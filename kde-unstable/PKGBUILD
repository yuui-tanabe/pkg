# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kde-l10n
pkgver=16.03.80
pkgrel=1
arch=('any')
url='http://www.kde.org/'
license=('GPL')
makedepends=('extra-cmake-modules' 'kdoctools' 'kdelibs' 'automoc4' 'kconfig' 'ki18n' 'qt5-tools')
options=('docs')

_languages=(
  'ar     "Arabic"'
  'ast    "Asturian"'
  'bg     "Bulgarian"'
  'bs     "Bosnian"'
  'ca     "Catalan"'
  'ca@valencia     "Catalan (Valencian)"'
  'cs     "Czech"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en_GB  "British English"'
  'eo     "Esperanto"'
  'es     "Spanish"'
  'et     "Estonian"'
  'eu     "Basque"'
  'fa     "Farsi"'
  'fi     "Finnish"'
  'fr     "French"'
  'ga     "Irish"'
  'gl     "Galician"'
  'he     "Hebrew"'
  'hi     "Hindi"'
  'hr     "Croatian"'
  'hu     "Hungarian"'
  'ia     "Interlingua"'
  'id     "Indonesian"'
  'is     "Icelandic"'
  'it     "Italian"'
  'ja     "Japanese"'
  'kk     "Kazakh"'
  'km     "Khmer"'
  'ko     "Korean"'
  'lt     "Lithuanian"'
  'lv     "Latvian"'
  'mr     "Marathi"'
  'nb     "Norwegian Bokmal"'
  'nds    "Low Saxon"'
  'nl     "Dutch"'
  'nn     "Norwegian Nynorsk"'
  'pa     "Panjabi/Punjabi"'
  'pl     "Polish"'
  'pt     "Portuguese"'
  'pt_BR  "Brazilian Portuguese"'
  'ro     "Romanian"'
  'ru     "Russian"'
  'sk     "Slovak"'
  'sl     "Slovenian"'
  'sr     "Serbian"'
  'sv     "Swedish"'
  'tr     "Turkish"'
  'ug     "Uyghur"'
  'uk     "Ukrainian"'
  'wa     "Walloon"'
  'zh_CN  "Chinese Simplified"'
  'zh_TW  "Chinese Traditional"'
)

pkgname=()
source=()

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=kde-l10n-${_locale,,}
  
  pkgname+=($_pkgname)
  source+=("http://download.kde.org/stable/applications/$pkgver/src/$pkgbase/$pkgbase-$_locale-$pkgver.tar.xz")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

md5sums=('a11c3874b73f75c88561ac88025d07f0'
         'a451c0bb4270c1a588135e73ffc221ac'
         '43e771a3aabdbf4b7d43c5fe4f44ff99'
         'dab9bef089f3cf156b0e6dd9ac491e79'
         '103f62367a937e9a075ef665a9f9ffd6'
         '3114ec370b5260b602f0b79ce7b3ce38'
         '9e3681b8358f8348ff071dcbdf5cc28e'
         '13ecb0713c863f0411cf4c92060c2144'
         'ad86cee9b18ea2b9198ec711eca06251'
         '835684f10528ce5c22dc5bd3abfdd189'
         '07bc6eb1b891b9e74bc1049d74e1ba23'
         'bb26f89a5908f4c3b95ece9bbe3cbb58'
         'e4a82af52b6af429880c7ad370e88516'
         'a342dfc833ba8ca6bbf13ca387648799'
         '61de1c7f98599e7f225b50183ced8e15'
         'b6751f3935f1cb7c3b0776d3a32bb167'
         '7fdc5e5b3bcdc819343f8c07ad4aab23'
         'f6b42e66210945e7afa9b1dce3005419'
         '9ab5334e3aeaebf499e143d6cb220076'
         'd006406eff5565d4c52afd1081d3fd1a'
         '9e6ce0a5ce9b0e6b4db28b3e900f80bc'
         '0c3988cb8d7f247002b5f305bd17f2c3'
         '0981b902882dc567b83eb8a81bab3b89'
         '59d81b6afc31703e5487e61d5a20cec6'
         'd24324d41fd8013651706070597c3c66'
         '16c0a4618e4ca0e0d94236e9d2a79147'
         '1121f23d3ecb52c06f414b3531460633'
         '19fd9e0b5c47c4250f7e60e72e777331'
         'c34c52a23d4267e6133a22b1032f9414'
         'b9a953c0cc60cc6b14dc0eb0353ff48e'
         'dc04d133d27bf8739683422f8071166e'
         'e6ec045d7225e3ab7cee7505d25e26d9'
         '2246f65263c1b4a86125549a1f8d798e'
         '81fcad32ca19c3414b5d5a6905df7fcc'
         'cf67597000126239ad2b785705c0367c'
         '692535c51618438e428e00233bfa466a'
         '2e68aa429630aa2f324a126c8c1ea405'
         '2878c785b2bee23cbf0183a582c0efde'
         'c58896e2fd2a9cb7a50bf65c308a1f9d'
         'a273fdf8e7482f37891c63c8625a29eb'
         '5f64abd707feaf9b3b351d61c641973a'
         '07c3fef927986d4ae3162679bedbcf56'
         '07f67b385b3556d263058605980147b9'
         '37c1422e2edcdebc4c1fa6127b443d01'
         'c27d5f7892204a872aec7035c4a56718'
         '20a940a38086485a54ab6d5fa43ca489'
         '200feeb42fefc1b9e2f1285d145e1882'
         'c535491285f04dd53009ffa6dcbbf43b'
         '0df2079395e4b1682a268a31812e8315'
         '06049572608b8f353c4506f4f4908e79'
         'e8e32f6534890d0bbbc421797b4139ef'
         '80b6e7edbda72d2adf240a0eee8ea1b4'
         'bb8b217d7ac52f8a14cbaf5dfd81d950'
         'ca441ed141310e02e56de1394d2d31d2'
         '7e43f40a4b178ccbf2448c4c58e5ed56')

prepare() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}

# Remove kde-runtime and kde-workspace docs, conflicts with plasma-desktop
  sed -e '/kde-runtime/d' -e '/kde-workspace/d' -i $pkgbase-$_locale-$pkgver/4/$_locale/docs/CMakeLists.txt || true

  done
}

build() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}
    
    mkdir -p build/$_locale
    cd build/$_locale
    cmake $srcdir/$pkgbase-$_locale-$pkgver \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr
    make
    cd ../..
  done
}

_package() {
  pkgdesc="$2 localization for KDE"
  cd build/$1
  make DESTDIR="$pkgdir" install
}
