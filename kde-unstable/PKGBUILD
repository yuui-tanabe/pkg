# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kde-l10n
pkgver=16.03.90
pkgrel=1
arch=('any')
url='http://www.kde.org/'
license=('GPL')
makedepends=('extra-cmake-modules' 'kdoctools' 'kdelibs' 'automoc4' 'kconfig' 'ki18n' 'qt5-tools' 'python')
options=('docs')

_languages=(
  'ar     "Arabic"'
  'ast    "Asturian"'
  'bg     "Bulgarian"'
  'bs     "Bosnian"'
  'ca     "Catalan"'
  'ca@valencia     "Catalan (Valencian)"'
  'cs     "Czech"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en_GB  "British English"'
  'eo     "Esperanto"'
  'es     "Spanish"'
#  'et     "Estonian"'
  'eu     "Basque"'
  'fa     "Farsi"'
  'fi     "Finnish"'
  'fr     "French"'
  'ga     "Irish"'
#  'gl     "Galician"'
  'he     "Hebrew"'
  'hi     "Hindi"'
  'hr     "Croatian"'
  'hu     "Hungarian"'
  'ia     "Interlingua"'
  'id     "Indonesian"'
  'is     "Icelandic"'
  'it     "Italian"'
  'ja     "Japanese"'
  'kk     "Kazakh"'
  'km     "Khmer"'
  'ko     "Korean"'
  'lt     "Lithuanian"'
  'lv     "Latvian"'
  'mr     "Marathi"'
  'nb     "Norwegian Bokmal"'
  'nds    "Low Saxon"'
  'nl     "Dutch"'
  'nn     "Norwegian Nynorsk"'
  'pa     "Panjabi/Punjabi"'
  'pl     "Polish"'
  'pt     "Portuguese"'
  'pt_BR  "Brazilian Portuguese"'
  'ro     "Romanian"'
  'ru     "Russian"'
  'sk     "Slovak"'
  'sl     "Slovenian"'
  'sr     "Serbian"'
  'sv     "Swedish"'
  'tr     "Turkish"'
  'ug     "Uyghur"'
  'uk     "Ukrainian"'
  'wa     "Walloon"'
  'zh_CN  "Chinese Simplified"'
  'zh_TW  "Chinese Traditional"'
)

pkgname=()
source=()

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=kde-l10n-${_locale,,}
  
  pkgname+=($_pkgname)
  source+=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgbase/$pkgbase-$_locale-$pkgver.tar.xz")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

md5sums=('11a2a4395881097b319d1be7acf2c627'
         'c4390c7a6b1e9d7d85de7a08f95d6948'
         'bf7184fdaf4e65df737f0b6f3489b096'
         '78d22ff516d230c53df567bf656b1e4f'
         '79d5ac4ed3c30863cfcf93a67e97e4b5'
         '4b717a94674ce44463508998495c5605'
         'd33f6ab13dad26c16616d85c055f2f1b'
         '05bf265d94886cee91b9d0639f8e901f'
         '288a9812c700f76325229d2492cc247d'
         'e18bb1f165d04c6401cae8c0670d79b2'
         'fca22aa9cac5fb699ca411733f79a263'
         '479a0d11935c371bc4825f1572cedacd'
         '2281fe5964985765ad8ef55b00106419'
         '0b39261063c09b9b9f4c25159e2bb01e'
         'ea92f863c976f6c1b060bf0228834fad'
         'a2a6ce47bc083ca23f7daada5cfd5aa4'
         '644fa12a7d2d39d997f1469c6fcbaa6c'
         '956eec542c105067ea2437bb20797264'
         'c996c46b4f0baf067ac9ac43df9beeec'
         'bd721f87b8f663fa24ed7acabf3eb638'
         '96a0559a3ee90a3e0e776d83e68c785d'
         '6dc1c8f4bfd3d445d57193b12a862e98'
         'ff7156222ec3308cbe3c2318dd96a904'
         'd2b46e1f8f26887e813e59fc28fcaf4e'
         '9321900d40a26d30c7bcd72cb6b6d292'
         '724660b3c02e505feee13fbbc1ad0c87'
         '08a4567bc4bd96de0235fcedf2354e81'
         'd94dd10866b6cc4e855cdd00abea20ea'
         'd9bfbdc98f147aaea39b97bc94536787'
         '6903cbf0e46d3eba025a7e57d37d678f'
         '7bebf4ece98661234f7463a935b174ae'
         'f9da2ad06e964a28d3f71c6294c02c59'
         '4aa6d3831e7b5ec626d8984fc8412932'
         '1262e1962df0b66f50b04ee5fa396cc2'
         'f6e3c3464c7f2f96a6029c826c3ef6b9'
         '16897e6d498355ab82ffc533041b1a73'
         '429ab30c88a01beead7a984a8580b7fd'
         'fa97001f1e91d03e1f23c91ecdf2a60f'
         '42ac1830d8dfa5e8c7dd981894e68a0d'
         '2785e33b1b91fabe211469c6958eeeca'
         '5072fabbba86424a3ae7e8c7fb02f52e'
         '9bc0fc108e07caa96d52d537873e5fae'
         'e1f5a651214eb9b5fb2d42e9ebc1ffbf'
         '77e46d787496c258fde755a1eb56f870'
         'dcbed92613ef0b5fba1c01eeac7d57f7'
         '141dc4cb2504e54bb081aff70113be5d'
         'd6fa43d254f4ac649355b6170c723d37'
         '9a9016bc38212225472216546dcd2a38'
         '0c22ebd0f043da861c95a0bc1b135190'
         'b83a392f635d9ead5c49ba98b8f6b7f9'
         '3c1dcc6ddb8a40dc578450f3777edfab'
         'a05a49e7c9c598b69831431f042d351b'
         '9adb27f3a19177e61f83ad62a096f7d6')

prepare() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}

# Remove kde-runtime and kde-workspace docs, conflicts with plasma-desktop
  sed -e '/kde-runtime/d' -e '/kde-workspace/d' -i $pkgbase-$_locale-$pkgver/4/$_locale/docs/CMakeLists.txt || true

  done
}

build() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}
    
    mkdir -p build/$_locale
    cd build/$_locale
    cmake $srcdir/$pkgbase-$_locale-$pkgver \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr
    make
    cd ../..
  done
}

_package() {
  pkgdesc="$2 localization for KDE"
  cd build/$1
  make DESTDIR="$pkgdir" install
}
