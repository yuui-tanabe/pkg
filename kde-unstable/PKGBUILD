# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kde-l10n
pkgver=15.11.80
pkgrel=1
arch=('any')
url='http://www.kde.org/'
license=('GPL')
makedepends=('extra-cmake-modules' 'kdoctools' 'kdelibs' 'automoc4' 'kconfig' 'ki18n' 'qt5-tools')
options=('docs')

_languages=(
  'ar     "Arabic"'
  'bg     "Bulgarian"'
  'bs     "Bosnian"'
  'ca     "Catalan"'
  'ca@valencia     "Catalan (Valencian)"'
  'cs     "Czech"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en_GB  "British English"'
  'eo     "Esperanto"'
  'es     "Spanish"'
  'et     "Estonian"'
  'eu     "Basque"'
  'fa     "Farsi"'
  'fi     "Finnish"'
  'fr     "French"'
  'ga     "Irish"'
  'gl     "Galician"'
  'he     "Hebrew"'
  'hi     "Hindi"'
  'hr     "Croatian"'
  'hu     "Hungarian"'
  'ia     "Interlingua"'
  'id     "Indonesian"'
  'is     "Icelandic"'
  'it     "Italian"'
  'ja     "Japanese"'
  'kk     "Kazakh"'
  'km     "Khmer"'
  'ko     "Korean"'
  'lt     "Lithuanian"'
  'lv     "Latvian"'
  'mr     "Marathi"'
  'nb     "Norwegian Bokmal"'
  'nds    "Low Saxon"'
#  'nl     "Dutch"'
  'nn     "Norwegian Nynorsk"'
  'pa     "Panjabi/Punjabi"'
  'pl     "Polish"'
  'pt     "Portuguese"'
  'pt_BR  "Brazilian Portuguese"'
  'ro     "Romanian"'
  'ru     "Russian"'
  'sk     "Slovak"'
  'sl     "Slovenian"'
  'sr     "Serbian"'
  'sv     "Swedish"'
  'tr     "Turkish"'
  'ug     "Uyghur"'
  'uk     "Ukrainian"'
  'wa     "Walloon"'
  'zh_CN  "Chinese Simplified"'
  'zh_TW  "Chinese Traditional"'
)

pkgname=()
source=()

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=kde-l10n-${_locale,,}
  
  pkgname+=($_pkgname)
  source+=("http://download.kde.org/stable/applications/$pkgver/src/$pkgbase/$pkgbase-$_locale-$pkgver.tar.xz")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

md5sums=('4566b89a5fa92ec4ce40a313929feb22'
         '5807e21a454e23376e39445ece7927af'
         '49eb5026ed9b859ced6a1d91338fdc4c'
         'c2c8ba95b85dad702635ff61c33b6352'
         'c1cd8cb94ce0b0aec192650f6a8110cc'
         '7a7c4fd17771769176a3133fc207a56e'
         '7c2dd1dfb09dd425c3d6cbd5a368ad75'
         'ec4f1f195a8a14b3420c069193f9a151'
         'a49191de8bf7ab52f9d4e635bf48adf7'
         '3cb640f82bd30cb7064389ece06a4863'
         '20d37cd3fd8496865df638b2a64b0864'
         '37074abc8469949b90ab231f871a5fe4'
         '7ea1c09183ff15773ca88e34d1afe212'
         '54e299db8642a5e2cd3f8aa5f16c8465'
         'a8e0a26df2d10dbb565c0f0e6e124a96'
         'f6cebc2cc600c68d46591d062f9846dd'
         '66c1863e0c49ada0d58fdfd9517b1a0b'
         '04b7b0daa593ad38503d70c21ab11d83'
         'e0fa8e8610faca6796e43a8efe72bc34'
         'bf0f42436f5d5f92efbb8fc8d86257de'
         '15febc51af34e4cac04b072fb2bbca59'
         '5ff46e71f8b5cfb8e2e2ac9a872d6fd0'
         'a1bd542f610da770ecf5dd3f74b92410'
         '501a737dd85625e4ecbdbba8e44d8998'
         '640717432bb5bbdccf12f86ac787344c'
         '91e3bc7f2756e5d5336474c07228c251'
         '1dff1b2f6efedc396af9b2f45d7cbe43'
         '241f56cfcbed2a4b381a68dda7596f74'
         '21e13fe00be2357a80bcd8b909eb14b1'
         'dbbc0c87fccb67d6adf413d96f0f4aa6'
         'c30becbbfbf1c262451e9b39d91fe4f6'
         'd4529d872080c16fb7705e28a80c6d30'
         'd3a1978852173b13bb55848a40a07b0a'
         '20196d557ed334b73837bdf386ce63e5'
         '1a9fb1aff8524119b099b6b2faf02654'
         'd53ed067f7e3e4f8cb75fcc7421b6843'
         'a0a70ff64b6567b87dc9f9933d6d6926'
         '0bce540de310c5b8206dc1a9643336ec'
         'be5c58db1381babfa477d0484610c810'
         'ee662ef7fdb151d2344b30e0af6222dc'
         '0c5b984d44fd3e83cf8a85e643bb3fb9'
         '35c863b82867b0aa9666853bc0a8dfdd'
         '1e286aa718b74e49ba81e880528e9c7d'
         '644d04e4c39e796bd4804207ee6c3abc'
         '1e2e00594df8c6129b36429c536ab013'
         '3f74bc90dc6af6a33690588d70f46c6b'
         '0d89b9a450b0938a5035a246945d40ff'
         '1d84b645747ccd8a36e3f2345d1a9b24'
         '00bc3e685d33bd89f053e7150d3fcb95'
         '1c6125a59e0cffd69d366e4307f80d76'
         'cdda73f213504e614f2a587b0fbbc513'
         '6a8ed720b586482bdc8e75d49c34cda3'
         '9383dde3bfea02f2de6f5aaabdf31812')

prepare() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}

# Remove kde-runtime and kde-workspace docs, conflicts with plasma-desktop
  sed -e '/kde-runtime/d' -e '/kde-workspace/d' -i $pkgbase-$_locale-$pkgver/4/$_locale/docs/CMakeLists.txt || true

  done
}

build() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}
    
    mkdir -p build/$_locale
    cd build/$_locale
    cmake $srcdir/$pkgbase-$_locale-$pkgver \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr
    make
    cd ../..
  done
}

_package() {
  pkgdesc="$2 localization for KDE"
  cd build/$1
  make DESTDIR="$pkgdir" install
}
