# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgbase=mutagen
pkgname=('mutagen' 'python-mutagen')
pkgver=1.34
pkgrel=1
arch=('any')
url="https://github.com/quodlibet/mutagen"
license=('GPL2')
makedepends=('python' 'python2')
source=($pkgname-$pkgver::"https://github.com/quodlibet/mutagen/archive/release-$pkgver.tar.gz")
sha1sums=('3c24aa5912bec93e1f8ec00c42f03cc2b913c7d4')
validpgpkeys=('0EBF782C5D53F7E5FB02A66746BD761F7A49B0EC')

prepare() {
  cp -r ${pkgbase}-release-${pkgver} python-${pkgbase}-${pkgver}
}

check() {
  cd ${pkgbase}-release-${pkgver}
  LANG=en_US.UTF8 python2 setup.py test
  cd ../python-${pkgbase}-${pkgver}
  LANG=en_US.UTF8 python setup.py test
}

package_mutagen() {
  pkgdesc="An audio metadata tag reader and writer"
  depends=('python2')
 
  cd ${pkgbase}-release-${pkgver}
  python2 setup.py install --root="${pkgdir}"
}

package_python-mutagen() {
  pkgdesc="An audio metadata tag reader and writer library for Python 3"
  depends=('python')

  cd python-${pkgbase}-${pkgver}
  python setup.py install --root="${pkgdir}"
  rm -r "${pkgdir}"/usr/{bin,share}
}
