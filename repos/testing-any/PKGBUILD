# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kde-l10n
pkgver=15.12.2
pkgrel=1
arch=('any')
url='http://www.kde.org/'
license=('GPL')
makedepends=('extra-cmake-modules' 'kdoctools' 'kdelibs' 'automoc4' 'kconfig' 'ki18n' 'qt5-tools')
options=('docs')

_languages=(
  'ar     "Arabic"'
  'bg     "Bulgarian"'
  'bs     "Bosnian"'
  'ca     "Catalan"'
  'ca@valencia     "Catalan (Valencian)"'
  'cs     "Czech"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en_GB  "British English"'
  'eo     "Esperanto"'
  'es     "Spanish"'
  'et     "Estonian"'
  'eu     "Basque"'
  'fa     "Farsi"'
  'fi     "Finnish"'
  'fr     "French"'
  'ga     "Irish"'
  'gl     "Galician"'
  'he     "Hebrew"'
  'hi     "Hindi"'
  'hr     "Croatian"'
  'hu     "Hungarian"'
  'ia     "Interlingua"'
  'id     "Indonesian"'
  'is     "Icelandic"'
  'it     "Italian"'
  'ja     "Japanese"'
  'kk     "Kazakh"'
  'km     "Khmer"'
  'ko     "Korean"'
  'lt     "Lithuanian"'
  'lv     "Latvian"'
  'mr     "Marathi"'
  'nb     "Norwegian Bokmal"'
  'nds    "Low Saxon"'
  'nl     "Dutch"'
  'nn     "Norwegian Nynorsk"'
  'pa     "Panjabi/Punjabi"'
  'pl     "Polish"'
  'pt     "Portuguese"'
  'pt_BR  "Brazilian Portuguese"'
  'ro     "Romanian"'
  'ru     "Russian"'
  'sk     "Slovak"'
  'sl     "Slovenian"'
  'sr     "Serbian"'
  'sv     "Swedish"'
  'tr     "Turkish"'
  'ug     "Uyghur"'
  'uk     "Ukrainian"'
  'wa     "Walloon"'
  'zh_CN  "Chinese Simplified"'
  'zh_TW  "Chinese Traditional"'
)

pkgname=()
source=()

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=kde-l10n-${_locale,,}
  
  pkgname+=($_pkgname)
  source+=("http://download.kde.org/stable/applications/$pkgver/src/$pkgbase/$pkgbase-$_locale-$pkgver.tar.xz")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

md5sums=('92c67c6d731c2dad881fc584567eb16a'
         '07ffb50f47bce34bb6685e0707e01ec9'
         '9b956c5e95de50bed827b8bba344247c'
         '7b0aa134ab85a27a2e0a14bd2321afe2'
         '649a1d41c5dc43c8bf6fc711f7914df0'
         'bdd9b38650e8e19d18406f079b3e5e73'
         '152ffc0b728fca6dda2ffda5e709c952'
         '529a85c3862e9499d9bc0d9270043012'
         'cded2473ba62f41c1c431aa79f5cffd2'
         'edc49e1a3ffaae5c54ec66ff27bd2c0c'
         '2661a0b12bf839a44a5a75526f188dc3'
         'b1aab057e9f5dc479daeaf77a9359063'
         'c13ac8b0c708e695016f8308719fb38c'
         'e9a14dd28cc5338e1245b6e80d97a678'
         'e7112deb60b8f12183cf6f5478365a36'
         '9d5686de677caff5697995692ca4ae5a'
         '123b752316713f5f343a783b571e6b1d'
         '75740680c88d481cec6376071c2e6254'
         '63897d7914c87f862855af325b947218'
         '82803195931fa5865823f1ed447fd58d'
         'cd663ac1a86aa68c74f608793ff6b7a2'
         '05df8ef985465ebb2645110d0e8dca7d'
         '9f129061b019d57f7f9e05fd7b02e739'
         '870d746f4efa7e4bcfbd897b18dd29b4'
         '2d6ab830bd6ff835953ca1f79b661312'
         '844ebac0ad0b3a28984d3a15652aaec0'
         'e38bc66e83ee7e2d8a3e5547b888e8c3'
         '9792b8f2266057a41b9be11d69f99d8c'
         '02b251bfa80e80fc858a3a1e5cdc1902'
         '264e8528e06634f0ac6c78e9d3c1ddd3'
         '80d22625c5fb7515c5e150d251f9cd4a'
         'ff9a44ad2cc1a102e3d52f1fde414b26'
         '961a96052a8f2f30e34abda7e0cfa3cf'
         '28af0c5561b6a9da7bc60e65ceca61d6'
         '03d1293ae3a10de1b3a9c6db2a262881'
         '01b9fbe746db6a7f61f8d9afddf939e8'
         '26035b152e25aeae9805e8e765dc642b'
         'd99efe028fe5cdc9750be855b3923b3b'
         '47580c17460864910683bc57515a6161'
         '4caa390473917e711a0e5f42a73e3f88'
         '64bc58656853865a27f653969a497bc2'
         '62681a1b95607120b0f0bb48756c3e2d'
         'd3e8a24089ca53d0a0c7a612f24d6f6e'
         '664cf1d8d49a6ad2cf6a1b10f1921340'
         'a6ee9be5825ad85ce5210faf271452d6'
         '897ccf4a7da90fee806e08a10c2c008d'
         'cdde71c38d6f03343d6f9af5d023922d'
         '24325662358225fd01ad720bd00e5ce6'
         '4c76f02b2e60fe8a1d918ef7e8f45457'
         '95d65fa3470f272d281f650a0bdf7ec4'
         '0142ee90b37e75b6d22797291878b855'
         'dcd4a262574c530b78041a62ac14fe98'
         '0f850aec2b28a8e7184dfe09c9b23d19'
         'c851a83540cc53a32e5a2ca2ffb6d30e')

prepare() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}

# Remove kde-runtime and kde-workspace docs, conflicts with plasma-desktop
  sed -e '/kde-runtime/d' -e '/kde-workspace/d' -i $pkgbase-$_locale-$pkgver/4/$_locale/docs/CMakeLists.txt || true

  done
}

build() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}
    
    mkdir -p build/$_locale
    cd build/$_locale
    cmake $srcdir/$pkgbase-$_locale-$pkgver \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr
    make
    cd ../..
  done
}

_package() {
  pkgdesc="$2 localization for KDE"
  cd build/$1
  make DESTDIR="$pkgdir" install
}
