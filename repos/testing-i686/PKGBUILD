# $Id$
# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=go-tools
epoch=2
pkgver=1.8.r2071.5682db0e
pkgrel=2
pkgdesc='Developer tools for the Go programming language'
arch=(i686 x86_64)
url='http://golang.org/'
license=(BSD)
depends=(glibc)
makedepends=(git go)
_commit=5682db0e919ed9cfc6f52ac32e170511a106eb3b
_net_commit=a6577fac2d73be281a500b310739095313165611
source=(git+https://go.googlesource.com/tools#commit=$_commit
        git+https://github.com/golang/net#commit=$_net_commit)
md5sums=('SKIP'
         'SKIP')

_tools=(benchcmp callgraph cover digraph eg fiximports godex godoc goimports
        gomvpkg gorename gotype goyacc guru heapview html2article present
        ssadump stress stringer)

pkgver() {
  local _gover=1.8
  cd tools
  printf "%s.r%s.%s" $_gover "$(git rev-list --count HEAD)" \
    "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p gopath/src/golang.org/x build
  cp -r tools net gopath/src/golang.org/x/
}

build() {
  export GOPATH="$srcdir/gopath"

  cd build
  for tool in ${_tools[@]}; do
    go build -v -a golang.org/x/tools/cmd/$tool
  done
}

check() {
  export GOPATH="$srcdir/gopath"

  for tool in ${_tools[@]}; do
    go test golang.org/x/tools/cmd/$tool
  done
}

package() {
  for tool in ${_tools[@]}; do
    install -Dm755 build/$tool "$pkgdir/usr/bin/$tool"
  done

  install -Dm644 tools/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
