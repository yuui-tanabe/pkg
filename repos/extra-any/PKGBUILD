# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kde-l10n
pkgver=16.12.0
pkgrel=2
arch=('any')
url='http://www.kde.org/'
license=('GPL')
depends=('qt5-translations') # needed for RTL languages FS#50780
makedepends=('extra-cmake-modules' 'kdoctools' 'kdelibs' 'automoc4' 'kconfig' 'ki18n' 'qt5-tools' 'python')
options=('docs')

_languages=(
  'ar     "Arabic"'
  'ast    "Asturian"'
  'bg     "Bulgarian"'
  'bs     "Bosnian"'
  'ca     "Catalan"'
  'ca@valencia     "Catalan (Valencian)"'
  'cs     "Czech"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en_GB  "British English"'
  'eo     "Esperanto"'
  'es     "Spanish"'
  'et     "Estonian"'
  'eu     "Basque"'
  'fa     "Farsi"'
  'fi     "Finnish"'
  'fr     "French"'
  'ga     "Irish"'
  'gl     "Galician"'
  'he     "Hebrew"'
  'hi     "Hindi"'
  'hr     "Croatian"'
  'hu     "Hungarian"'
  'ia     "Interlingua"'
  'id     "Indonesian"'
  'is     "Icelandic"'
  'it     "Italian"'
  'ja     "Japanese"'
  'kk     "Kazakh"'
  'km     "Khmer"'
  'ko     "Korean"'
  'lt     "Lithuanian"'
  'lv     "Latvian"'
  'mr     "Marathi"'
  'nb     "Norwegian Bokmal"'
  'nds    "Low Saxon"'
  'nl     "Dutch"'
  'nn     "Norwegian Nynorsk"'
  'pa     "Panjabi/Punjabi"'
  'pl     "Polish"'
  'pt     "Portuguese"'
  'pt_BR  "Brazilian Portuguese"'
  'ro     "Romanian"'
  'ru     "Russian"'
  'sk     "Slovak"'
  'sl     "Slovenian"'
  'sr     "Serbian"'
  'sv     "Swedish"'
  'tr     "Turkish"'
  'ug     "Uyghur"'
  'uk     "Ukrainian"'
  'wa     "Walloon"'
  'zh_CN  "Chinese Simplified"'
  'zh_TW  "Chinese Traditional"'
)

pkgname=()
source=()

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=kde-l10n-${_locale,,}
  
  pkgname+=($_pkgname)
  source+=("http://download.kde.org/stable/applications/$pkgver/src/$pkgbase/$pkgbase-$_locale-$pkgver.tar.xz")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

md5sums=('dba3a0fe8ee4a2f1ca916778a42451a2'
         '0bb51f6e2054466d25bfe2c6af479772'
         '8f312b4385ce0faed13a810e04c6928b'
         '859d4b8e9b818f08dfc6e6d16615825a'
         '76c7925a1012ef8ff23fcdf7fee15855'
         '3eda1f72dc49cc2b79662d378d180b2e'
         '61734ddd5b915069194cee85517e2d8e'
         '65bbfd86992c79aada4424def933f6f4'
         '0aede2e49532c78baac04ab8df59e622'
         '131899f0041fc992f06c22cdd7d6e42a'
         '195da0a39c159d66ef9f643b86c5196e'
         'd68e694677bafb55478a40b0baba89cc'
         '965ab549c482429a0085724f80afd3f6'
         '18572f80f61f7d777646008c6fede349'
         '2d3d76557fb62bd81bb4067d171cfe3c'
         '8be3646b5a63b5ec87c8d92a12e9ebec'
         'd9dccedfedc2dbb8b29171aa94471c5b'
         'c7bc7611ad893aa49206e580011f5029'
         'b1f94b8cac63fe29fe826479bf54e0b9'
         '496ac2ee203440ed465d067ad33ae397'
         'eddae34286666292bbbb95d1b599ba77'
         '5e5031113af4b04533b731dfc27256e0'
         '69b618981f256af477f2bf01e5f4cdcc'
         'e0cc5159927403c331aa1fddee522403'
         '731c742aab3b226c8ab81ccb155ebf41'
         'a1de83d3da34be50164b5435fadaece0'
         'fb20ccb82af337eb3399bd35903d55e0'
         '38f0854dc974ab4a5fb04e9b59b30ab0'
         'de95d996b3088112dd56f5b8b034f512'
         'de7f95e6b149c3f09c49737698e87291'
         'd520c63e51ce9217ec07ebafc4ca6d89'
         '91e372741b28cbfeac4a2a3bd29b803f'
         'fab4268f20eadcd9cfcb5015952c1127'
         '496c1e7fdaadc2c0b7c93a24ead7677c'
         'd61cb5759ef9a1ce6ff9dcfd8ca004b8'
         '6510e4c074010a505ba7344b6936c941'
         '54f54fc3c901d27515bcc570f5982de3'
         'a41305fea053302ff16a96554048a392'
         '6ed402c5d155181815c0dc1000d9a09a'
         'ff96f9d45b88ca29500c5545b2f92c53'
         '2ed2e53251f348e9cd1d8c40c7f8f9c4'
         'ac710b85d4a491ec8f13ccd519486f69'
         '063d8f651567230dfae6fa15a80dc086'
         'fc2b9248d1691cb61fc760b6e6617723'
         '06226993cfcee74d98ba91a22c5570c0'
         '777a53dca066f01d49d355e27515e442'
         '4d794a42533d5c3048a13a882e397787'
         'a680eb81350fc627f56c29da40f897d5'
         '2902b6919a96435fbc7a5d2811cce4de'
         '53c3882fd6b58d1964e8dc215de95f3b'
         '72fa37f66a6c36666b30f142ccd56f37'
         '476a74107031247a69ac72f4b1c4abe9'
         '0d0b0a29020f1c1e7ebd0ee1d16b2a58'
         '6f95561e0b03a77e571e793abdf4b35e'
         'bcf95fbb61a1f9aedcbc5d6df6e7ca8a')

build() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}
    
    mkdir -p build/$_locale
    cd build/$_locale
    cmake $srcdir/$pkgbase-$_locale-$pkgver \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr
    make
    cd ../..
  done
}

_package() {
  pkgdesc="$2 localization for KDE Applications"
  cd build/$1
  make DESTDIR="$pkgdir" install

# Fix conflicts
  rm -fr "$pkgdir"/usr/share/doc/HTML/*/{kdesu,knetattach,kcontrol/kcm_ssl}
}
