# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kde-l10n
pkgver=15.12.1
pkgrel=1
arch=('any')
url='http://www.kde.org/'
license=('GPL')
makedepends=('extra-cmake-modules' 'kdoctools' 'kdelibs' 'automoc4' 'kconfig' 'ki18n' 'qt5-tools')
options=('docs')

_languages=(
  'ar     "Arabic"'
  'bg     "Bulgarian"'
  'bs     "Bosnian"'
  'ca     "Catalan"'
  'ca@valencia     "Catalan (Valencian)"'
  'cs     "Czech"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en_GB  "British English"'
  'eo     "Esperanto"'
  'es     "Spanish"'
  'et     "Estonian"'
  'eu     "Basque"'
  'fa     "Farsi"'
  'fi     "Finnish"'
  'fr     "French"'
  'ga     "Irish"'
  'gl     "Galician"'
  'he     "Hebrew"'
  'hi     "Hindi"'
  'hr     "Croatian"'
  'hu     "Hungarian"'
  'ia     "Interlingua"'
  'id     "Indonesian"'
  'is     "Icelandic"'
  'it     "Italian"'
  'ja     "Japanese"'
  'kk     "Kazakh"'
  'km     "Khmer"'
  'ko     "Korean"'
  'lt     "Lithuanian"'
  'lv     "Latvian"'
  'mr     "Marathi"'
  'nb     "Norwegian Bokmal"'
  'nds    "Low Saxon"'
  'nl     "Dutch"'
  'nn     "Norwegian Nynorsk"'
  'pa     "Panjabi/Punjabi"'
  'pl     "Polish"'
  'pt     "Portuguese"'
  'pt_BR  "Brazilian Portuguese"'
  'ro     "Romanian"'
  'ru     "Russian"'
  'sk     "Slovak"'
  'sl     "Slovenian"'
  'sr     "Serbian"'
  'sv     "Swedish"'
  'tr     "Turkish"'
  'ug     "Uyghur"'
  'uk     "Ukrainian"'
  'wa     "Walloon"'
  'zh_CN  "Chinese Simplified"'
  'zh_TW  "Chinese Traditional"'
)

pkgname=()
source=()

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=kde-l10n-${_locale,,}
  
  pkgname+=($_pkgname)
  source+=("http://download.kde.org/stable/applications/$pkgver/src/$pkgbase/$pkgbase-$_locale-$pkgver.tar.xz")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

md5sums=('5dc4ed371d8c24297fd6375c070ffb4b'
         '4375304eac54f0569bddd4a0dc5a68c7'
         'b28e1d822398e53b8080272b5e2aa798'
         '18405c311154795427a5da4e5657367c'
         'f06cdfbc301ad1e1888fba5c23a6c3ab'
         '78341e92647885d215fc22772879717e'
         '5abb4aa212409dbb9cdac73933b2d030'
         '74451b6dc755168e44a128f1dd123d4a'
         'e7ac1ea2a3b86e3c78110065d87032aa'
         '0c3fb10ee1434f997530eb72721f5016'
         'a30e8402583a207f4c731169cae54ea6'
         '99f8180e60abdcb4826b15a88c31ab33'
         '1f01c7774d605ea515c8bd79459203f9'
         '87ca351216b2fe93d8b5992f8073be67'
         '96abaa25be115f77eeec811152b3906b'
         'f8068a4415c8cc75c18dc17aae0301f7'
         '3af7f0e98982fbf8c05874ba796969ad'
         '0f99d56de9f6fb0523434fd02fbf9125'
         'caab52ef2c9ac36a1717445364f25cb3'
         'f43e993e34cdbfcb0a280905ba120366'
         'a0bdf60e57019916054ba6962e607bcb'
         'bf59ce98e185bfc63a5a2db9eba5d2f3'
         'd14f38ade61878d754fc4c5716b8e968'
         '3b9b77b40b223e53fe571131d1448f58'
         'b4ae52fe6c4bc918689056a02c2896f9'
         '83b6a69c6f43eb7ff0cc568c844bad31'
         '76c3d66f5c62fff10a8cbb0b7eb9c7d7'
         '8cbf898ec7989fa7e514223a22d54477'
         '111fb3d97081142f0060882956beadb8'
         '837b2d8ad4ae271b1b3c2b33008e3f66'
         'b4c0bfdcfd41fdd378934aef839372a2'
         '6018beed9dc5e96ef591a1cc4d4b7239'
         '729ceff92dc7e2a7d35a13721eb1b5f1'
         '8b7a3a54158663cfd1dbfd8df65b5632'
         '5c4f4c8a2f21b548d8f5b776811b6eba'
         'fc170582fd50e9a64ef78fa12ab2f6af'
         '4201d2c75e0af15944a7a112da468469'
         '399eea451ff3bf0d4d6acaef05c95237'
         'ec107f23d738f1a84794e9749f82870d'
         '67842f906da970e6b398740433f788b8'
         'ed17488d0527fedfa9b986627e445ef5'
         '9f5325b9c1c9bcfe8c8a10a28599c637'
         '5e142dea946d86e0a9fdfd79e7b811c3'
         'df20f42b234b6fe66e2bba323ffaa8a4'
         '256fc37716a31a30def63b3e5f0c6f08'
         'aefc9edef8636d7c374807fbd08b00bd'
         'be4cd0c84394300964099673fcb36a96'
         '6deeb5ddc84af9b7cc386d3919876f29'
         '6ab469d89ecc4d53a8b77d92b8a49ecb'
         'c396733613c85314db1395078390c52c'
         '003783ac5158922c0c7686a8895bff47'
         '2371e1c06dba3bf7eb346774ee5b664d'
         '78d51d193419ba6ecf62afdac0e1defd'
         '112c0be95dc93e313b3da97a371ac453')

prepare() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}

# Remove kde-runtime and kde-workspace docs, conflicts with plasma-desktop
  sed -e '/kde-runtime/d' -e '/kde-workspace/d' -i $pkgbase-$_locale-$pkgver/4/$_locale/docs/CMakeLists.txt || true

  done
}

build() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}
    
    mkdir -p build/$_locale
    cd build/$_locale
    cmake $srcdir/$pkgbase-$_locale-$pkgver \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr
    make
    cd ../..
  done
}

_package() {
  pkgdesc="$2 localization for KDE"
  cd build/$1
  make DESTDIR="$pkgdir" install
}
