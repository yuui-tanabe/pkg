# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=gajim
pkgver=0.16.9
pkgrel=1
pkgdesc="A full featured and easy to use Jabber client"
arch=('any')
url="https://www.gajim.org/"
license=('GPL3')
depends=('pygtk' 'python2-pyopenssl' 'python2-pyasn1' 'python2-nbxmpp' 'ca-certificates' 'bind-tools')
makedepends=('intltool')
optdepends=('python2-dbus: dbus support'
            'gtkspell: for spelling support'
            'libxss: for idle module'
            'notification-daemon: for desktop notification'
            'python2-gnomekeyring: for GnomeKeyring support'
            'python2-crypto: support for E2E encryption'
            'python2-docutils: for RST generator support'
            'gupnp-igd: for UPnP-IGD support'
            'python2-gnupg: for PGP encryption')
source=(https://www.gajim.org/downloads/${pkgver%.*}/gajim-${pkgver}.tar.bz2)
sha1sums=('5e329aa769cc098ffeea94894bd8b4ad200293a1')

build() {
  cd ${pkgname}-${pkgver}
  PYTHON=python2 ./configure --prefix=/usr --enable-site-packages
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
