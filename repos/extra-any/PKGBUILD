# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kde-l10n
pkgver=16.08.0
pkgrel=1
arch=('any')
url='http://www.kde.org/'
license=('GPL')
makedepends=('extra-cmake-modules' 'kdoctools' 'kdelibs' 'automoc4' 'kconfig' 'ki18n' 'qt5-tools' 'python')
options=('docs')

_languages=(
  'ar     "Arabic"'
  'ast    "Asturian"'
  'bg     "Bulgarian"'
  'bs     "Bosnian"'
  'ca     "Catalan"'
  'ca@valencia     "Catalan (Valencian)"'
  'cs     "Czech"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en_GB  "British English"'
  'eo     "Esperanto"'
  'es     "Spanish"'
  'et     "Estonian"'
  'eu     "Basque"'
  'fa     "Farsi"'
  'fi     "Finnish"'
  'fr     "French"'
  'ga     "Irish"'
  'gl     "Galician"'
  'he     "Hebrew"'
  'hi     "Hindi"'
  'hr     "Croatian"'
  'hu     "Hungarian"'
  'ia     "Interlingua"'
  'id     "Indonesian"'
  'is     "Icelandic"'
  'it     "Italian"'
  'ja     "Japanese"'
  'kk     "Kazakh"'
  'km     "Khmer"'
  'ko     "Korean"'
  'lt     "Lithuanian"'
  'lv     "Latvian"'
  'mr     "Marathi"'
  'nb     "Norwegian Bokmal"'
  'nds    "Low Saxon"'
  'nl     "Dutch"'
  'nn     "Norwegian Nynorsk"'
  'pa     "Panjabi/Punjabi"'
  'pl     "Polish"'
  'pt     "Portuguese"'
  'pt_BR  "Brazilian Portuguese"'
  'ro     "Romanian"'
  'ru     "Russian"'
  'sk     "Slovak"'
  'sl     "Slovenian"'
  'sr     "Serbian"'
  'sv     "Swedish"'
  'tr     "Turkish"'
  'ug     "Uyghur"'
  'uk     "Ukrainian"'
  'wa     "Walloon"'
  'zh_CN  "Chinese Simplified"'
  'zh_TW  "Chinese Traditional"'
)

pkgname=()
source=()

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=kde-l10n-${_locale,,}
  
  pkgname+=($_pkgname)
  source+=("http://download.kde.org/stable/applications/$pkgver/src/$pkgbase/$pkgbase-$_locale-$pkgver.tar.xz")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

md5sums=('92103905145c89e98d3189689bb5d0ae'
         '6700dfa3c9462c2d790e525efdb8cbed'
         'e45a0550caa0e0e552116557aefedd31'
         '3117f6bb0b413b955a37af60838675b4'
         '1d03b11c22e0511a90024ee56fc64645'
         '9c69d1682dd715ebd78470c1d42562ed'
         '683edaf6dc1a11c6dd10515ca5793fa2'
         'edceb743472cf82a66e7f185b91227de'
         'd650ec76b22e1a4b12e9dc1c1f75135c'
         '5e283f9e4956f305d03785133d900094'
         '12411da965bbe05c83e47b277004dbd2'
         '817fc2f0c1a9cd398e496e9b50c5164b'
         '1d15768f581b1f66de0edc42c9393e1c'
         '58706be160d34f914c2e6665c87e3c0e'
         '2ddec375ca3d06686c453968de84f96a'
         '6fa60535d1ff232d0aa362379d1dd78c'
         '1936bfee6b9ca42d55d6d17e27e18515'
         '3add3a1153ecfea5b62e106d9c0118bc'
         '52cc7ffcf304851a531b8b30c5a6df73'
         'dad3cdcf9ccb515d17c9b53c1fef14bf'
         '337108310d6964bf583608dbf15ca5dc'
         '04955f6b89f83a0cb1a440884a9a16c3'
         '6891c6cd00ae6399c8944cbc89891ee9'
         '7c2c0e044bcacd138543ea419bd713e7'
         '3f63e088011ebcde9df7d77ea5611c66'
         '8db201be9e15c2eb7512f0483c545590'
         'e02ec168f558fc19f8df2692e7d3bb3a'
         'c588e88f9225441cd7990fe26ddd7d4e'
         '37a96dfcb062588727a998f08f414541'
         'be47adc03e81846b21f34e7f711315e9'
         'b22abe6d57cc3d2ce25032c0e0df9eb7'
         '936c90c41e4ba55c2bda6b6d1db2dc55'
         '4a4b1774e23ab99fa695b3d62322431b'
         '7ca324e130c43ecaedcacdab77b7eee4'
         '898df27e3938e0b4ce9a9fe92207a449'
         'b30de5beeffc3f03ef76a20d3c218b18'
         '576a779bcc5d950393b0a94e8fc4fea0'
         '0b90d658b35598452950dbfd209feddf'
         '7291239ee9c70c8750165efc7b00c66d'
         '2eabdd71d49cf3166e1a0930289a829e'
         '4ea6116add632aeaa72056cc6f511c81'
         '45e5323c09874ee8ecf5c9437fae48ec'
         '987fb1e0c1c481fe2246fd821113fa4c'
         'c26964b41b8c4714c0f41f746fdcd45e'
         '8e1e3ef649d980756126e86393c99237'
         '95875ef8c7ad16349559c8aba35e7412'
         'f2074158016a3b6d2b3792b5f22adb02'
         '9f16c4c7f0dfc28d2f360245d518b074'
         'b65f5b1f8f45037493a09e84ad389179'
         'b82cf1105b30d1d8f3e09bb61316eac4'
         'c208bf1c18ce207fd026ff8450f8bc74'
         '9419d8dc75742884e185d12c3bb8cdca'
         'f40cf1d5ed64608202123325eb7f176a'
         'e4821565072c4978c8f8b0a0c410600e'
         '3c4865c7f86a351dc6e03bb48bfae31c')

prepare() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}

# Remove kde-runtime and kde-workspace docs, conflicts with plasma-desktop
  sed -e '/kde-runtime/d' -e '/kde-workspace/d' -e '/kdeplasma-addons/d' \
   -i $pkgbase-$_locale-$pkgver/4/$_locale/docs/CMakeLists.txt || true
# kde-l10n-sr needs special casing
  sed -e '/kde-runtime/d' -e '/kde-workspace/d' -e '/kdeplasma-addons/d' \
   -i $pkgbase-$_locale-$pkgver/4/$_locale/*/docs/CMakeLists.txt || true
  done
}

build() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}
    
    mkdir -p build/$_locale
    cd build/$_locale
    cmake $srcdir/$pkgbase-$_locale-$pkgver \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr
    make
    cd ../..
  done
}

_package() {
  pkgdesc="$2 localization for KDE Applications"
  cd build/$1
  make DESTDIR="$pkgdir" install
}
