# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kde-l10n
pkgver=15.12.3
pkgrel=1
arch=('any')
url='http://www.kde.org/'
license=('GPL')
makedepends=('extra-cmake-modules' 'kdoctools' 'kdelibs' 'automoc4' 'kconfig' 'ki18n' 'qt5-tools')
options=('docs')

_languages=(
  'ar     "Arabic"'
  'bg     "Bulgarian"'
  'bs     "Bosnian"'
  'ca     "Catalan"'
  'ca@valencia     "Catalan (Valencian)"'
  'cs     "Czech"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en_GB  "British English"'
  'eo     "Esperanto"'
  'es     "Spanish"'
  'et     "Estonian"'
  'eu     "Basque"'
  'fa     "Farsi"'
  'fi     "Finnish"'
  'fr     "French"'
  'ga     "Irish"'
  'gl     "Galician"'
  'he     "Hebrew"'
  'hi     "Hindi"'
  'hr     "Croatian"'
  'hu     "Hungarian"'
  'ia     "Interlingua"'
  'id     "Indonesian"'
  'is     "Icelandic"'
  'it     "Italian"'
  'ja     "Japanese"'
  'kk     "Kazakh"'
  'km     "Khmer"'
  'ko     "Korean"'
  'lt     "Lithuanian"'
  'lv     "Latvian"'
  'mr     "Marathi"'
  'nb     "Norwegian Bokmal"'
  'nds    "Low Saxon"'
  'nl     "Dutch"'
  'nn     "Norwegian Nynorsk"'
  'pa     "Panjabi/Punjabi"'
  'pl     "Polish"'
  'pt     "Portuguese"'
  'pt_BR  "Brazilian Portuguese"'
  'ro     "Romanian"'
  'ru     "Russian"'
  'sk     "Slovak"'
  'sl     "Slovenian"'
  'sr     "Serbian"'
  'sv     "Swedish"'
  'tr     "Turkish"'
  'ug     "Uyghur"'
  'uk     "Ukrainian"'
  'wa     "Walloon"'
  'zh_CN  "Chinese Simplified"'
  'zh_TW  "Chinese Traditional"'
)

pkgname=()
source=()

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=kde-l10n-${_locale,,}
  
  pkgname+=($_pkgname)
  source+=("http://download.kde.org/stable/applications/$pkgver/src/$pkgbase/$pkgbase-$_locale-$pkgver.tar.xz")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

md5sums=('da776204f42f8cbed28ab47b2b323dc9'
         '4047bd53572211cb40a3b438f1d5a782'
         'f86e8fda5a8b0c1f69c01a92c6180b4b'
         'bcac362e35f60b2b288bb969e39a3c50'
         '929837c702dfe97de1dbbbbd57ab2fce'
         '317bf58a9b21b97318224fc5ea99b022'
         'd14d53709ffe23cf8f8ecee0640b094f'
         '0b5db199ae20df6505d85661899979ff'
         '641a1784ff19eff4c93a3c7c5f36b82d'
         '42030339e285e751c91c17ad9c5941a0'
         '1fe60b3837f5d94d78a02aef5ced56fb'
         '780590aaf3e1285a99ebd02888d62990'
         '81a78eca30b12d89cacc2543a95d09ab'
         '8d588c27c481db2f4086786a42de101c'
         '6123bc18fb5ee54121f31c2044525e88'
         '5b5c9b9df4b1712d66abed3ce683d807'
         'ba1a3adf5922c543a6f54f488353182e'
         '1df3e468a6635ccf7af7a96970a777b6'
         '6b8d14c9294672943b63b183fa5ffd95'
         'fdc3ba6da0cfb597aeef2cc4a6e4deb4'
         '887ea5c7280babdf03c6d780bc69b99d'
         '071ac58235fd573f989b5d95d02af6d7'
         'cb1e17c37d12e6a71c93558c53acc4e4'
         '2a6a26512c57c845bdc45f4c44db50a9'
         '8d3818f8503be11ca244a2b4693f91f4'
         '070ec4b0d9702b52737740235606e9c3'
         '5eb796567ff9a4aed4379f9831267ff8'
         '157dbee294eae3fc8d2262be1780a496'
         '758cfc14518ada4e9eefd37e70057755'
         '6e9524ff9fb6f3d4ed481fbe8bcc4a56'
         '41f5a43878af055011e679fe8d132a51'
         'a8ad5fd5343d9866163e2e889717a71d'
         '79113bb1fa1778ed9e76775d7c141553'
         '7ddabcf8d0b33d846ca599d5f7905500'
         '90a168ecb4be6752912da69150c6353d'
         '76383b1fefd2a93ac806443e53761cc0'
         '6a7606edab2a0e6ecb141667cb516be7'
         'e173280166f3eec07e656ea17708be0e'
         '90d258e011314fc8c1626825e9892c8b'
         '9dcaf27dc6709a91f853fa0b46dfd75c'
         '9bf97bff2ce02c550ddecc66a9bf6bf7'
         'ca82679c2a2076c745965dbf272ec556'
         '7b4b8c3ce2cd85de9804176257eba985'
         'de9df54243e730ddda462aa9aa2bf4ab'
         '34762d861463f9d7e3bde720ed51b2ca'
         'e42895b4d81ca00e468ac9865377e316'
         'c681ebadc18ce9fcfc58144574537470'
         '64881ddcf603ad2958221b3533a2efd7'
         'fa4cbdbd44d20b2b1c93858178ec9025'
         '3f7f6b6a25e002418dfa085c3dfbdcac'
         'b4b9abf45e4f4171a6bff80d5c3bf10b'
         'aa3430cf860600d146165b0baf6ae43a'
         '5f703a4875cd42bb6abf62147c9e5dec'
         '95eb08c8802cb4b25fadeba0ca31a4ad')

prepare() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}

# Remove kde-runtime and kde-workspace docs, conflicts with plasma-desktop
  sed -e '/kde-runtime/d' -e '/kde-workspace/d' -i $pkgbase-$_locale-$pkgver/4/$_locale/docs/CMakeLists.txt || true

  done
}

build() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}
    
    mkdir -p build/$_locale
    cd build/$_locale
    cmake $srcdir/$pkgbase-$_locale-$pkgver \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr
    make
    cd ../..
  done
}

_package() {
  pkgdesc="$2 localization for KDE"
  cd build/$1
  make DESTDIR="$pkgdir" install
}
