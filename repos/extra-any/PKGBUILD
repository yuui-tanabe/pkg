# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgbase=pylint
pkgname=('python-pylint' 'python2-pylint')
pkgver=1.6.4.20170104
_commit=da1da56853380a5a387ad287f4398402b14ef123
pkgrel=1
pkgdesc="Analyzes Python code looking for bugs and signs of poor quality"
arch=('any')
url="http://pylint.org"
license=('GPL')
makedepends=('python2-astroid' 'python-astroid' 'python2-setuptools' 'python-setuptools'
             'python2-six' 'python-six' 'python-isort' 'python2-isort' 'python-mccabe'
             'python2-mccabe' 'python2-backports.functools_lru_cache' 'python2-configparser'
             'python-editdistance' 'python2-editdistance' 'git')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
optdepends=('tk: Pylint GUI')
#source=("git+https://github.com/PyCQA/pylint.git#tag=pylint-$pkgver")
source=("git+https://github.com/PyCQA/pylint.git#commit=$_commit")
sha1sums=('SKIP')

prepare() {
  cp -a pylint{,-py2}

  cd "$srcdir"/pylint/pylint
  sed -e "s|/usr/bin/env python|/usr/bin/env python3|" -e "s|/usr/bin/python$|/usr/bin/python3|" -i epylint.py __main__.py test/input/noext test/data/ascript

  cd "$srcdir"/pylint-py2/pylint
  sed -e "s|/usr/bin/env python|/usr/bin/env python2|" -e "s|/usr/bin/python$|/usr/bin/python2|" -i epylint.py __main__.py test/input/noext test/data/ascript
}

build() {
  cd "$srcdir"/pylint
  python setup.py build

  cd "$srcdir"/pylint-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/pylint
  python setup.py ptr || warning "Tests failed"

  cd "$srcdir"/pylint-py2
  python2 setup.py ptr
}

package_python-pylint() {
  depends=('python-astroid' 'python-setuptools' 'python-editdistance' 'python-mccabe' 'python-isort')

  cd pylint

  python3 setup.py install --prefix=/usr --root="$pkgdir" --skip-build --optimize=1

  install -d "$pkgdir"/usr/share/man/man1
  install -m644 man/* "$pkgdir"/usr/share/man/man1
}

package_python2-pylint() {
  depends=('python2-astroid' 'python2-setuptools' 'python2-editdistance' 'python2-mccabe' 'python2-isort'
           'python2-backports.functools_lru_cache' 'python2-configparser')

  cd pylint-py2

  python2 setup.py install --prefix=/usr --root="$pkgdir" --skip-build --optimize=1

  install -d "$pkgdir"/usr/share/man/man1
  install -m644 man/* "$pkgdir"/usr/share/man/man1

  for _exe in epylint pylint pyreverse symilar; do
     mv "$pkgdir"/usr/bin/${_exe}{,2}
     mv "$pkgdir"/usr/share/man/man1/${_exe}{,2}.1
  done

  mv "$pkgdir"/usr/share/man/man1/pylint-gui{,2}.1
}
