# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgbase=nbxmpp
pkgname=('python-nbxmpp' 'python2-nbxmpp')
pkgver=0.6.3
pkgrel=1
arch=('any')
url="https://dev.gajim.org/gajim/python-nbxmpp/"
license=('GPL3')
makedepends=('python' 'python2')
source=($pkgbase-$pkgver.tar.gz::https://dev.gajim.org/gajim/python-nbxmpp/repository/$pkgbase-$pkgver/archive.tar.gz)
sha1sums=('80d44536f48c4d6a44ca6b92eced64edbb3ca963')

prepare() {
  cp -r python-nbxmpp-* ${pkgbase}-${pkgver}
  cp -r python-nbxmpp-* ${pkgbase}-${pkgver}-python2
}

build() {
  cd ${pkgbase}-${pkgver}-python2
  python2 setup.py build
  cd ../${pkgbase}-${pkgver}
  python setup.py build
}

package_python-nbxmpp() {
  pkgdesc="A Python 3 library to use Jabber/XMPP networks in a non-blocking way"
  depends=('python')
  cd ${pkgbase}-${pkgver}
  python setup.py install --root="${pkgdir}"
}

package_python2-nbxmpp() {
  pkgdesc="A Python 2 library to use Jabber/XMPP networks in a non-blocking way"
  depends=('python2')
  cd ${pkgbase}-${pkgver}-python2
  python2 setup.py install --root="${pkgdir}"
}
