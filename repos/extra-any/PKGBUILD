# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kde-l10n
pkgver=16.08.3
pkgrel=1
arch=('any')
url='http://www.kde.org/'
license=('GPL')
depends=('qt5-translations') # needed for RTL languages FS#50780
makedepends=('extra-cmake-modules' 'kdoctools' 'kdelibs' 'automoc4' 'kconfig' 'ki18n' 'qt5-tools' 'python')
options=('docs')

_languages=(
  'ar     "Arabic"'
  'ast    "Asturian"'
  'bg     "Bulgarian"'
  'bs     "Bosnian"'
  'ca     "Catalan"'
  'ca@valencia     "Catalan (Valencian)"'
  'cs     "Czech"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en_GB  "British English"'
  'eo     "Esperanto"'
  'es     "Spanish"'
  'et     "Estonian"'
  'eu     "Basque"'
  'fa     "Farsi"'
  'fi     "Finnish"'
  'fr     "French"'
  'ga     "Irish"'
  'gl     "Galician"'
  'he     "Hebrew"'
  'hi     "Hindi"'
  'hr     "Croatian"'
  'hu     "Hungarian"'
  'ia     "Interlingua"'
  'id     "Indonesian"'
  'is     "Icelandic"'
  'it     "Italian"'
  'ja     "Japanese"'
  'kk     "Kazakh"'
  'km     "Khmer"'
  'ko     "Korean"'
  'lt     "Lithuanian"'
  'lv     "Latvian"'
  'mr     "Marathi"'
  'nb     "Norwegian Bokmal"'
  'nds    "Low Saxon"'
  'nl     "Dutch"'
  'nn     "Norwegian Nynorsk"'
  'pa     "Panjabi/Punjabi"'
  'pl     "Polish"'
  'pt     "Portuguese"'
  'pt_BR  "Brazilian Portuguese"'
  'ro     "Romanian"'
  'ru     "Russian"'
  'sk     "Slovak"'
  'sl     "Slovenian"'
  'sr     "Serbian"'
  'sv     "Swedish"'
  'tr     "Turkish"'
  'ug     "Uyghur"'
  'uk     "Ukrainian"'
  'wa     "Walloon"'
  'zh_CN  "Chinese Simplified"'
  'zh_TW  "Chinese Traditional"'
)

pkgname=()
source=()

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=kde-l10n-${_locale,,}
  
  pkgname+=($_pkgname)
  source+=("http://download.kde.org/stable/applications/$pkgver/src/$pkgbase/$pkgbase-$_locale-$pkgver.tar.xz")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

md5sums=('fb2dab04a916f6637dbf3026736f434e'
         '2cd72f8c096562dcb77e51894c404505'
         '29af49753d359b632c3ae7eb70ba9806'
         '83bbc2bea0a994e1f6100d6cdb8b7897'
         'ef91a9c9beee2ecf42053ad0ec5487a9'
         'ecfa9fa0b2b032d513c1ef53956e5837'
         'e74c2811f7b2c96540060fbb952089fd'
         '4aed47eddf271c5cd8a85b47f02bbc68'
         '9e5a5c7d2802d63cc62f917430503e96'
         '5901cdaeca5539d41f19553e33c4ff3f'
         '80dc3e637b6326055dc86d186cd7864d'
         '42199df84bbde0afc3ca4a18fc8d2737'
         'd6b3267a88d4219922d7583c13da0f8a'
         '371d792ae9cc8adc78ce393c4f1df98d'
         'e6dbc4d56ad5f2120c0bcf40ccd9ee5d'
         '3ad2bb2379da3dd1826f1701e9428ec9'
         '74c5487564c4d094c75eab083336c07d'
         'ece1c94a29bd1c14d3118e2ecfd8f111'
         '068810e40ad79c28472f79d64814dfe5'
         '97c11900db6220f7bd696c82be43faf3'
         '216dcf96ca3adf0cf7149f2db0520420'
         '961c3a3274e5a20567d6a077279ae3ea'
         '041b57ebad2d616605405a51f3390c15'
         'ad530185591359fddb52d233ad7c9f8f'
         '393054dd95e7ff1d44bf05bee949a8aa'
         'a91d4fafa927097bf8f3c0edf4fde864'
         'f20772afd58b73a12b2cca6e44e74d63'
         'b666bc97a02f010d45f2893de5a207dd'
         '00679d865f77b07628d5369dee6f9fc0'
         '6ea7416a9c4d25cca125b45c7a30a6fc'
         '55d2f60e623601f58cc6577b6c87f308'
         '236c8cdb110af2c30ee1b70895b35a9d'
         'cd4cab8c8773de7c332c38581bf3ec16'
         'c468716964d44f5c9da3934571c454b6'
         'cc4c1ba17c48e56849d678240f84e8e5'
         '9b3659860a09f5ae59e1b7570ca11f6a'
         '7334a08249d872e3b9e41fe2fad87f7e'
         'a54f9d9cc87fcfcdee75dd7535ea4fba'
         'fd1834ae478f39b0fdf547a85225d6bb'
         'e9e8a9f8f836857790f4339fb04a9af5'
         '650ec1a6c0979e22e099f824d25f32d2'
         '98453d7010bb733c19961011d617edb5'
         'fb0761c043188b3e7d08f226b157a42e'
         'b210b607d9f01b0106503dcea7a6446c'
         '876f32b5636100718c531a54bccb809d'
         'b10c494153f728a2baab2e2a3716ab11'
         '4d9e1ee4a66c618058ba52d4415cd2fd'
         'd6b54ed230db0c086ddd0b551f11fb13'
         '04a984e5119a6544669a3fe044b963b1'
         'f18b2924e21ae1b6d130b2fdde933e88'
         '6b22a70e086656933410918ebbb1e846'
         '608dae624e9c25f0cac5a33b60ab004b'
         'aa3399bf48fc5b222f79f4ec1c5f0acc'
         '627649e73564f6f845bc658a3a1c0e9a'
         'da70ebf58a022d124b713e698f3d87b9')

prepare() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}

# Remove kde-runtime and kde-workspace docs, conflicts with plasma-desktop
  sed -e '/kde-runtime/d' -e '/kde-workspace/d' -e '/kdeplasma-addons/d' \
   -i $pkgbase-$_locale-$pkgver/4/$_locale/docs/CMakeLists.txt || true
# kde-l10n-sr needs special casing
  sed -e '/kde-runtime/d' -e '/kde-workspace/d' -e '/kdeplasma-addons/d' \
   -i $pkgbase-$_locale-$pkgver/4/$_locale/*/docs/CMakeLists.txt || true
  done
}

build() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}
    
    mkdir -p build/$_locale
    cd build/$_locale
    cmake $srcdir/$pkgbase-$_locale-$pkgver \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr
    make
    cd ../..
  done
}

_package() {
  pkgdesc="$2 localization for KDE Applications"
  cd build/$1
  make DESTDIR="$pkgdir" install
}
