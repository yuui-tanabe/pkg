# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kde-l10n
pkgver=16.08.2
pkgrel=1
arch=('any')
url='http://www.kde.org/'
license=('GPL')
depends=('qt5-translations') # needed for RTL languages FS#50780
makedepends=('extra-cmake-modules' 'kdoctools' 'kdelibs' 'automoc4' 'kconfig' 'ki18n' 'qt5-tools' 'python')
options=('docs')

_languages=(
  'ar     "Arabic"'
  'ast    "Asturian"'
  'bg     "Bulgarian"'
  'bs     "Bosnian"'
  'ca     "Catalan"'
  'ca@valencia     "Catalan (Valencian)"'
  'cs     "Czech"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en_GB  "British English"'
  'eo     "Esperanto"'
  'es     "Spanish"'
  'et     "Estonian"'
  'eu     "Basque"'
  'fa     "Farsi"'
  'fi     "Finnish"'
  'fr     "French"'
  'ga     "Irish"'
  'gl     "Galician"'
  'he     "Hebrew"'
  'hi     "Hindi"'
  'hr     "Croatian"'
  'hu     "Hungarian"'
  'ia     "Interlingua"'
  'id     "Indonesian"'
  'is     "Icelandic"'
  'it     "Italian"'
  'ja     "Japanese"'
  'kk     "Kazakh"'
  'km     "Khmer"'
  'ko     "Korean"'
  'lt     "Lithuanian"'
  'lv     "Latvian"'
  'mr     "Marathi"'
  'nb     "Norwegian Bokmal"'
  'nds    "Low Saxon"'
  'nl     "Dutch"'
  'nn     "Norwegian Nynorsk"'
  'pa     "Panjabi/Punjabi"'
  'pl     "Polish"'
  'pt     "Portuguese"'
  'pt_BR  "Brazilian Portuguese"'
  'ro     "Romanian"'
  'ru     "Russian"'
  'sk     "Slovak"'
  'sl     "Slovenian"'
  'sr     "Serbian"'
  'sv     "Swedish"'
  'tr     "Turkish"'
  'ug     "Uyghur"'
  'uk     "Ukrainian"'
  'wa     "Walloon"'
  'zh_CN  "Chinese Simplified"'
  'zh_TW  "Chinese Traditional"'
)

pkgname=()
source=()

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=kde-l10n-${_locale,,}
  
  pkgname+=($_pkgname)
  source+=("http://download.kde.org/stable/applications/$pkgver/src/$pkgbase/$pkgbase-$_locale-$pkgver.tar.xz")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

md5sums=('d860a71c17931aa3b20df794469956a1'
         '09ceb68fa8cc8ccfe3dd5da61777d35b'
         '58bf191e04092f3720fe9077fabac5fc'
         '094e7e13ea242c6d484f9ab10994608a'
         'c601f6d713f06016661165abcf387386'
         'cee9e522c69d0868f0e04cf6484d70c1'
         '001c138518a7884a6ccd833de83caf38'
         'cf275427e24ae8221d7fa13a23867056'
         '93d451ab355fab35ddd2627302c14504'
         'f293c0c2d61bf261420d18d6ac5de2e1'
         'c8b356b3ab11bd01cba4b816a2440f3e'
         'fd0fe7f09f8ff4534cb48386863dc6d4'
         '081c176503078df7e327d6fdd9f2b687'
         'ac3b5f0d16c70214a81aaf6eca780695'
         'c38913352c3eccb973b32d6b6002b910'
         'a2d80fa4ed1b55ee24f96117c634ca29'
         '2e7c6300f29b4e766a445607e44e568f'
         '1b226c9bdb951cee29a4beeea286839a'
         '61a8a6e330bb92a99271ed8ca659303b'
         '2aea44f2bcd050835ce2309f5bb26be8'
         'c416ecd91acea8b9bf625933582759c2'
         '07d08c8b5d1c1218662b585c0a3c8999'
         '16eaecc3368a2538b8e3d75da534cf1e'
         '1fed40ad99612ee4e6896ddfd02e135b'
         'fe52408e28786a28ff09e1590cb0353a'
         '194c07c6efb8db3734b968b068aef23e'
         'a45aefbff94301e2d1cbc43681ff7913'
         'a5c06526c80ac7e4540e08f2dd96a002'
         '33ad9bb20421ccab9464ccbc34207dc1'
         '50882918f1817af278e02abd74f42304'
         '086b5312f3fe9cc083f7a82f44ee1f9a'
         '8cadde5ae11826faf9ca8ab63fd41cc2'
         'b556d65d96b8f9458838f07ce4e952be'
         '2903851c4aa2fbdf8eaf77e5d7ca2edc'
         '77ff9913e40c64d24a07874e188053a0'
         '107678621713af30f4e9051ed3fef66e'
         'aeb3b3fc90da1161faceae1dff12ef94'
         '75e73f20672a0f71626bad184991ddb0'
         '02a8dbf3b19c5bfff7e1ee5fda31f4ee'
         '789c7458e348b3b44c84eabbdb738b64'
         'c63000ab63fbba8082de6ca976d8b909'
         'b6dcd409bbca61df50f0405ff853d38a'
         '45c05863bd2da4cf31569aede6307ddf'
         '18b6978a58aeb0f12650c4ae1eda9464'
         'e9ed381b124c241b8da8f0e0aad0eb4d'
         'b872fcce05d02ce59f011370329bb029'
         '7d6e87ee34a8fafc1bf995e498c1d791'
         '68c4102d360df9f9269b839a4c8b84ff'
         '65f56d4949f88c8fa3bf76a94f36bdfc'
         '7e53508018ca17c33dfa3c6abbc48b86'
         '5807e457a59eb8601eaf21ac89ca0ed6'
         'ec3de03e505cced0dfb2090f7e777911'
         '0ed642426d6a11b1c56465a82c2280a7'
         'b582431061ef89dc95a26b43641e3350'
         'ec1075ecbecb534ae3ef922dc567ea31')

prepare() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}

# Remove kde-runtime and kde-workspace docs, conflicts with plasma-desktop
  sed -e '/kde-runtime/d' -e '/kde-workspace/d' -e '/kdeplasma-addons/d' \
   -i $pkgbase-$_locale-$pkgver/4/$_locale/docs/CMakeLists.txt || true
# kde-l10n-sr needs special casing
  sed -e '/kde-runtime/d' -e '/kde-workspace/d' -e '/kdeplasma-addons/d' \
   -i $pkgbase-$_locale-$pkgver/4/$_locale/*/docs/CMakeLists.txt || true
  done
}

build() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}
    
    mkdir -p build/$_locale
    cd build/$_locale
    cmake $srcdir/$pkgbase-$_locale-$pkgver \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr
    make
    cd ../..
  done
}

_package() {
  pkgdesc="$2 localization for KDE Applications"
  cd build/$1
  make DESTDIR="$pkgdir" install
}
