# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=gajim
pkgver=0.16.5
pkgrel=2
pkgdesc="A full featured and easy to use Jabber client"
arch=('any')
url="http://www.gajim.org/"
license=('GPL3')
depends=('pygtk' 'python2-pyopenssl' 'python2-pyasn1' 'python2-nbxmpp' 
         'ca-certificates' 'ldns' 'desktop-file-utils')
makedepends=('intltool')
optdepends=('python2-dbus: dbus support'
            'farstream-0.1: for video/voice support'
            'gstreamer0.10-bad: for video/voice support'
	    'gstreamer0.10-python: for video/voice support'
            'gtkspell: for spelling support'
            'libxss: for idle module'
            'notification-daemon: for desktop notification'
            'python2-gnomekeyring: for GnomeKeyring support'
            'python2-crypto: support for E2E encryption'
            'python2-docutils: for RST generator support'
            'gupnp-igd: for UPnP-IGD support')
source=(http://www.gajim.org/downloads/${pkgver%.*}/gajim-${pkgver}.tar.bz2 gajim-drill.patch)
sha1sums=('c310174be5becb85f46d4e2d6b90de3931846588'
          '4cb0e88acc310e010a892b2826a54528ec82b474')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}/gajim-drill.patch"
}

build() {
  cd ${pkgname}-${pkgver}
  PYTHON=python2 ./configure --prefix=/usr --enable-site-packages
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
