# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kde-l10n
pkgver=16.04.0
pkgrel=2
arch=('any')
url='http://www.kde.org/'
license=('GPL')
makedepends=('extra-cmake-modules' 'kdoctools' 'kdelibs' 'automoc4' 'kconfig' 'ki18n' 'qt5-tools' 'python')
options=('docs')

_languages=(
  'ar     "Arabic"'
  'ast    "Asturian"'
  'bg     "Bulgarian"'
  'bs     "Bosnian"'
  'ca     "Catalan"'
  'ca@valencia     "Catalan (Valencian)"'
  'cs     "Czech"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en_GB  "British English"'
  'eo     "Esperanto"'
  'es     "Spanish"'
  'et     "Estonian"'
  'eu     "Basque"'
  'fa     "Farsi"'
  'fi     "Finnish"'
  'fr     "French"'
  'ga     "Irish"'
  'gl     "Galician"'
  'he     "Hebrew"'
  'hi     "Hindi"'
  'hr     "Croatian"'
  'hu     "Hungarian"'
  'ia     "Interlingua"'
  'id     "Indonesian"'
  'is     "Icelandic"'
  'it     "Italian"'
  'ja     "Japanese"'
  'kk     "Kazakh"'
  'km     "Khmer"'
  'ko     "Korean"'
  'lt     "Lithuanian"'
  'lv     "Latvian"'
  'mr     "Marathi"'
  'nb     "Norwegian Bokmal"'
  'nds    "Low Saxon"'
  'nl     "Dutch"'
  'nn     "Norwegian Nynorsk"'
  'pa     "Panjabi/Punjabi"'
  'pl     "Polish"'
  'pt     "Portuguese"'
  'pt_BR  "Brazilian Portuguese"'
  'ro     "Romanian"'
  'ru     "Russian"'
  'sk     "Slovak"'
  'sl     "Slovenian"'
  'sr     "Serbian"'
  'sv     "Swedish"'
  'tr     "Turkish"'
  'ug     "Uyghur"'
  'uk     "Ukrainian"'
  'wa     "Walloon"'
  'zh_CN  "Chinese Simplified"'
  'zh_TW  "Chinese Traditional"'
)

pkgname=()
source=()

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=kde-l10n-${_locale,,}
  
  pkgname+=($_pkgname)
  source+=("http://download.kde.org/stable/applications/$pkgver/src/$pkgbase/$pkgbase-$_locale-$pkgver.tar.xz")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

md5sums=('1d6bc75ae11874b2e36b505015197fd4'
         'dfdeca2642c1d513d9475166685808ef'
         '27f833983e833f29ebe6dc6eb1c99555'
         '40106d88f6244aba085fa5dfc0e58353'
         '1bde56cc5634265f98ba5a2f604738c5'
         '24564c957f2236cd5e2f0e43ecdb24d3'
         '171a927fb9502a6dd41294070c6d39aa'
         'f98840d5383ddcfaa74432bfd88ab47c'
         '613eb9c654e18ae09cf100451f9c6bbe'
         'd28c3bfbaf5019090e4d4f38502cb3c6'
         '17189291b1515a0079e7d7182f03c184'
         'b8d6b713cfe55a67f1538eb555cc38e1'
         'c32feadaa17dedc07e90a975e04c3178'
         'dfc707dafe4a076a6e9ddb2537fcab39'
         '2e5a8a4e81033e4682df2fc6d00d7f68'
         '101633e9581ef2a2d5cbb53073930d27'
         '318d0fc886b11d6f33528a08c8b8ab36'
         '811e76aeb553214a41b3e6b90a6ac2f3'
         '8448b701db04c18ffb425db71763aaab'
         'ea0ee57f1407c0756def64127556cc1c'
         'a986abeaff664fe74f5dd31c9f52a3b4'
         'c4d707ccbbdb88edc220976ddfa18506'
         '34a56069e93dbfd4bdd251fd1fae68a1'
         '3377f29fe77dddb1d8ec16860e505a66'
         '6423331c8f2546c09918dc3955299b76'
         '5a436d2aa2cbfd74f91f3cae48006dc6'
         '34ff6fa6e13b10995ee46cb405318c5e'
         'db60f494730b136a6f5d211f69c8f56c'
         'a11c57ca50925190dd9272608a8b3c8d'
         '8027547220886583554ae3c57633b43e'
         '9c196fc297d0ce766c62440e16b0a88c'
         '237c6e209ea662d7c98928f3c9ce9357'
         'e64bb9a577caa52f35f34d60162317a6'
         '16b76e3370d840c678b4fc6da0d85d82'
         '3184b29e10c00e3ffcd0b64b05d79a2f'
         'c27df84f1c3929589cfe801a32d14857'
         '74ef7248f2c289ea28f67bc042481c1b'
         '395994727c90544edcfa4111e6707333'
         '36d6a3412d0af12af6afacb7ddc78a87'
         '86d96012c009a9f1f83025c8de6bd8dd'
         '54826fa75d8831e7d4e0443b09b98d36'
         'd2008d914c6a6b3a4b4f9a17dea5d2f7'
         '645a7edef294575d986822946c19fb55'
         '1da1765d3c01d83bcee10edbeea9d438'
         'f6eac7de17c9a80ef4fa1baa3c14fbc4'
         '708a1c2acd364337707911bd14513b80'
         'eb192360c67e065d3451a2ca2fb7f76c'
         'fd1eb82ad79a3ce7324ffb8daf7aeafd'
         '7e23a40c88ad73f2cdafe994a1aa4017'
         '24f227572741e501a724f210f22fe035'
         'd03ed87db5120be21617fa6163c0d2ea'
         'b23b653c0cecaaf003e11e3d8818321a'
         '72a9950bf2b72cda1f8cbba38834584e'
         '37d64ecac6f52be7f13e382ca16ebd68'
         'ac6628c8e3ac19c557dfbcd8b4dc037a')

prepare() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}

# Remove kde-runtime and kde-workspace docs, conflicts with plasma-desktop
  sed -e '/kde-runtime/d' -e '/kde-workspace/d' -e '/kdeplasma-addons/d' \
   -i $pkgbase-$_locale-$pkgver/4/$_locale/docs/CMakeLists.txt || true

  done
}

build() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}
    
    mkdir -p build/$_locale
    cd build/$_locale
    cmake $srcdir/$pkgbase-$_locale-$pkgver \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr
    make
    cd ../..
  done
}

_package() {
  pkgdesc="$2 localization for KDE"
  cd build/$1
  make DESTDIR="$pkgdir" install

# FS#48957
  rm -f "$pkgdir"/usr/share/locale/*/LC_SCRIPTS/ki18n5/ki18n5.js
# FS#49023
  rm -fr "$pkgdir"/usr/share/doc/HTML/*/kinfocenter
}
