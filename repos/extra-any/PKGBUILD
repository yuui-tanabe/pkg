# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=fonts-tlwg
pkgver=0.6.3
pkgrel=1
pkgdesc='Collection of scalable Thai fonts'
url='https://linux.thai.net/projects/fonts-tlwg'
arch=('any')
license=('GPL' 'custom')
makedepends=('fontforge')
depends=('xorg-fonts-encodings' 'xorg-fonts-alias' 'xorg-font-utils' 'fontconfig')
source=("https://linux.thai.net/pub/thailinux/software/${pkgname}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('89e98f61395a371c0b5e190a6905bd273aed2a47')

conflicts=('ttf-tlwg')
provides=('ttf-tlwg')
replaces=('ttf-tlwg')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--with-ttfdir=/usr/share/fonts/TTF \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
