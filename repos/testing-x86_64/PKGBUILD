# $Id$
# Maintainer:
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Denis Martinez <deuns.martinez AT gmail.com>

pkgname=intel-tbb
pkgver=2017_20170226
_pkgver=2017_U5
pkgrel=1
pkgdesc='High level abstract threading library'
arch=(i686 x86_64)
url='http://www.threadingbuildingblocks.org/'
license=(GPL)
depends=(gcc-libs)
source=(https://github.com/01org/tbb/archive/$_pkgver.tar.gz)
sha256sums=('780baf0ad520f23b54dd20dc97bf5aae4bc562019e0a70f53bfc4c1afec6e545')

build() {
  cd tbb-$_pkgver
  export CXXFLAGS+=" -fno-lifetime-dse" # FS#49898
  make
}

package() {
  cd tbb-$_pkgver
  install -d "$pkgdir"/usr/lib
  install -m755 build/linux_*/*.so* "$pkgdir"/usr/lib
  install -d "$pkgdir"/usr/include
  cp -a include/tbb "$pkgdir"/usr/include
}
