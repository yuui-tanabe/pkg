# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pkgname=ktnef
pkgname=lib$_pkgname
pkgver=15.12.2
pkgrel=1
pkgdesc="API for handling TNEF data"
arch=(i686 x86_64)
url="https://projects.kde.org/$pkgname"
license=(LGPL)
depends=(kcalutils kcontacts)
makedepends=(extra-cmake-modules python kdoctools)
source=("http://download.kde.org/stable/applications/$pkgver/src/$_pkgname-$pkgver.tar.xz")
md5sums=('aa3f1c5caf4896b05851c7a028df8665')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$_pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
