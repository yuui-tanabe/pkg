# $Id$
# Maintainer: Tobias Kieslich <neri@archlinux.org>
# Contributor: Ben <contrasutra@myrealbox.com>

pkgname=lcms
pkgver=1.19
pkgrel=6
pkgdesc='Lightweight color management development library/engine'
arch=('x86_64')
license=('custom')
depends=('libtiff')
url='http://www.littlecms.com'
source=("https://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'cve-2013-4276.patch')
sha256sums=('80ae32cb9f568af4dc7ee4d3c05a4c31fc513fc3e31730fed0ce7378237273a9'
            'cd10cc5ce791ae782b1257e6181a71cbdb685b705779c9ef2ceffc7fb2021bd0')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i ../cve-2013-4276.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
