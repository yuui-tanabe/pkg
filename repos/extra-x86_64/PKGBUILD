# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=xfig
pkgver=3.2.6
pkgrel=1
pkgdesc="An interactive drawing tool"
arch=('i686' 'x86_64')
url="http://mcj.sourceforge.net/"
license=('custom')
depends=('libjpeg' 'xaw3d' 'gsfonts')
optdepends=('fig2dev: to use the frontend to convert fig files')
options=('!makeflags' '!emptydirs')
source=(http://downloads.sourceforge.net/mcj/${pkgname}-${pkgver}.tar.xz
	xfig-aspell.patch xfig-3.2.5-color-resources.patch
        xfig-3.2.5-urwfonts.patch LICENSE)
sha1sums=('10872f47f0da53d7dc5e30507a8520bebf32068a'
          'a57fcc3c8396d58b19061f9cdd93beea728e38e5'
          'dd41e0a007bb74b74e1af50b6b04aa6aec61ab22'
          'a11af0a4edb7991fb81b6a2ff12fd33203a9dc45'
          '31edf4cfab708820ea3f114d095dfef5aa88e5aa')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}/xfig-aspell.patch"
  patch -p1 -i "${srcdir}/xfig-3.2.5-color-resources.patch"
  patch -p1 -i "${srcdir}/xfig-3.2.5-urwfonts.patch"
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make XFIGDOCDIR=/usr/share/doc/xfig LIBDIR=/usr/share \
    XAPPLOADDIR=/usr/share/X11/app-defaults
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" XFIGDOCDIR=/usr/share/doc/xfig LIBDIR=/usr/share \
    MANDIR=/usr/share/man/man1 XAPPLOADDIR=/usr/share/X11/app-defaults install

#  find "${pkgdir}/usr/share/xfig/Libraries" -type f -exec chmod 0644 {} \;
#  find "${pkgdir}/usr/share/xfig/Libraries" -type d -exec chmod 0755 {} \;
#  find "${pkgdir}/usr/share/doc/xfig" -type f -exec chmod 0644 {} \;
#  find "${pkgdir}/usr/share/doc/xfig" -type d -exec chmod 0755 {} \;

  install -D -m644 ../LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"	
}
