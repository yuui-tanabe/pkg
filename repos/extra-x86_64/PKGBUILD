# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>
# Contributor: simo <simo@archlinux.org>
# Contributor: FUBAR <mrfubar@gmail.com>

pkgname=bitlbee
pkgver=3.5
pkgrel=1
pkgdesc='Brings instant messaging (XMPP, MSN, Yahoo!, AIM, ICQ, Twitter) to IRC'
url='http://www.bitlbee.org/'
license=('GPL')
arch=('i686' 'x86_64')
depends=('gnutls' 'glib2')
makedepends=('python' 'libotr')
optdepends=('skype4py: to use skyped'
            'libotr: for OTR encryption support')
source=("https://get.bitlbee.org/src/$pkgname-$pkgver.tar.gz"
        'bitlbee.tmpfiles')
sha1sums=('f69e103b6ade7d32a02f8f86ff787120efdf16e7'
          '3695ed2fe22436c4d0fc3ead829f7d1f89bc491c')
backup=('etc/bitlbee/bitlbee.conf'
        'etc/bitlbee/motd.txt')
install=bitlbee.install

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --etcdir=/etc/bitlbee \
    --sbindir=/usr/bin \
    --pidfile=/run/bitlbee/bitlbee.pid \
    --ipcsocket=/run/bitlbee/bitlbee.sock \
    --systemdsystemunitdir=/usr/lib/systemd/system \
    --ssl=gnutls \
    --strip=0 \
    --otr=plugin \
    --skype=plugin

  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install{,-etc,-dev,-systemd}

  install -o65 -g65 -dm770 "$pkgdir/var/lib/bitlbee"
  install -Dm644 "$srcdir/bitlbee.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/bitlbee.conf"
}
