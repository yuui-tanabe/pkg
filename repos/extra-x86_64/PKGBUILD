# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pkgname=ktnef
pkgname=lib$_pkgname
pkgver=16.12.2
pkgrel=2
pkgdesc="API for handling TNEF data"
arch=(i686 x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(kcalutils kcontacts)
makedepends=(extra-cmake-modules python kdoctools kdesignerplugin)
source=("https://download.kde.org/stable/applications/$pkgver/src/$_pkgname-$pkgver.tar.xz"{,.sig}
        ktnef-directory-traversal.patch::"https://cgit.kde.org/ktnef.git/patch/?id=4ff38aa1")
md5sums=('a2e7c55dbe19efd90bc3e2dffe40216e'
         'SKIP'
         '6273402e57d311d8163faa69d3a8d371')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build

  # https://www.kde.org/info/security/advisory-20170227-1.txt
  cd $_pkgname-$pkgver
  patch -p1 -i ../ktnef-directory-traversal.patch
}

build() {
  cd build
  cmake ../$_pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
