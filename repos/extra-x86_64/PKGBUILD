# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdebase-runtime
pkgver=16.08.3
pkgrel=3
pkgdesc="Plugins and applications necessary for the running of KDE applications"
arch=('i686' 'x86_64')
url='https://www.kde.org/'
license=('GPL' 'LGPL')
makedepends=('pkg-config' 'cmake' 'automoc4' 'openslp' 'doxygen' 'networkmanager' 'boost')
depends=('kdelibs' 'smbclient' 'libssh' 'libcanberra' 'oxygen-icons' 'xorg-xauth' 'libwebp' 'exiv2' 'gpgme')
optdepends=('drkonqi: crash report tool' 'khelpcenter: to display applications documentation')
provides=(notification-daemon)
conflicts=(kdesu4)
replaces=(kdesu4)
source=("http://download.kde.org/stable/applications/${pkgver}/src/kde-runtime-${pkgver}.tar.xz" kdebase-runtime-gpgme-1.7.patch)
sha1sums=('d215765e6fd4da4e0e7a53bd414d276fd46417e6'
          'a9261cc182da51ed86155e5068a845b9d490b784')

prepare() {
  mkdir -p build

# Build against gpgme's gpgme++
  cd kde-runtime-$pkgver
  patch -p1 -i ../kdebase-runtime-gpgme-1.7.patch
# Don't build drkonqi
  sed -e '/drkonqi/d' -i CMakeLists.txt
# Don't build plasma stuff
  sed -e '/add_subdirectory(plasma)/d' -i CMakeLists.txt
}

build() {
  cd build
  cmake ../kde-runtime-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_QNtrack=OFF \
    -DSAMBA_INCLUDE_DIR=/usr/include/samba-4.0 \
    -DWITH_NepomukCore=OFF \
    -DBUILD_khelpcenter=OFF \
    -DBUILD_kglobalaccel=OFF \
    -DBUILD_kuiserver=OFF \
    -DBUILD_doc=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  rm "$pkgdir"/usr/share/icons/hicolor/index.theme
  ln -s /usr/lib/kde4/libexec/kdesu "$pkgdir"/usr/bin/kdesu4

# Use KF5 drkonqi
  ln -s /usr/lib/drkonqi "$pkgdir"/usr/lib/kde4/libexec/

# FS#36668
  chown :nobody "$pkgdir"/usr/lib/kde4/libexec/kdesud
  chmod g+s "$pkgdir"/usr/lib/kde4/libexec/kdesud
}
