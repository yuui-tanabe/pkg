# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=totem-plparser
pkgver=3.26.0+11+g0091a56
pkgrel=1
pkgdesc="Simple GObject-based library to parse and save a host of playlist formats"
url="https://git.gnome.org/browse/totem-pl-parser"
license=(LGPL)
arch=(x86_64)
depends=(gmime3 libarchive libquvi libxml2)
makedepends=(gobject-introspection git gtk-doc meson)
_commit=0091a562f2299fc61943bf5b44ecd7c472288693  # master
source=("git+https://git.gnome.org/browse/totem-pl-parser#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd totem-pl-parser
  git describe --tags | sed 's/^V_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd totem-pl-parser
}

build() {
  arch-meson totem-pl-parser build -D enable-gtk-doc=true
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
