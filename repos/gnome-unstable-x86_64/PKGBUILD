# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=totem-plparser
pkgver=3.10.7+1+gb25e497
pkgrel=1
pkgdesc="Simple GObject-based library to parse and save a host of playlist formats"
url="https://git.gnome.org/browse/totem-pl-parser"
license=(LGPL)
arch=(i686 x86_64)
depends=(gmime libsoup libarchive libquvi)
makedepends=(intltool gobject-introspection git gnome-common gtk-doc)
_commit=b25e4976c4409cdca22e456eb5679148b787556e
source=("git://git.gnome.org/totem-pl-parser#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd totem-pl-parser
  git describe --tags | sed 's/^V_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd totem-pl-parser
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd totem-pl-parser
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib --disable-static --enable-gtk-doc
  make
}

package(){
  cd totem-pl-parser
  make DESTDIR="$pkgdir" install
}
