# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Paul Mattal <paul@archlinux.org>
# Contributor: Andrew Wright <andreww@photism.org>
# Contributor: Andreas W. Hauser <andy-aur@splashground.de>
# Contributor: Marco Crosio <marco.crosio@gmail.com>

pkgbase=eclipse
pkgname=(eclipse-{common,java,jee,cpp,php})
pkgver=4.6.3
pkgrel=1
_release=neon-3
pkgdesc="Highly extensible IDE"
license=(EPL)
arch=(i686 x86_64)
url="https://eclipse.org"
makedepends=(ruby)
options=(!emptydirs)
source=(commonify eclipse.sh eclipse.desktop)
source_i686=()
source_x86_64=()
noextract=()

for _pkg in ${pkgname[@]}; do
  [[ $_pkg == 'eclipse-common' ]] && continue
  source_i686+=(http://ftp-stud.fht-esslingen.de/pub/Mirrors/eclipse/technology/epp/downloads/release/${_release/-//}/$_pkg-$_release-linux-gtk.tar.gz)
  source_x86_64+=(http://ftp-stud.fht-esslingen.de/pub/Mirrors/eclipse/technology/epp/downloads/release/${_release/-//}/$_pkg-$_release-linux-gtk-x86_64.tar.gz)
  noextract+=($_pkg-$_release-linux-gtk{,-x86_64}.tar.gz)
  eval "package_$_pkg() { _package $_pkg; }"
done

sha256sums=('9f8980078c97fb3020ba2e9c6c07eac077b932406aba3e2d6a4763a5d299290d'
            '04e789fb695d5f95d09ddb994ae30397b39aee35f11997465dd91d129c41b2ed'
            '57559c2548ae463089acb3c2825ebc002ed83067ddc953b23d36a7b5a02deaf3')
sha256sums_i686=('07452c284696ed79f056296c9c47f5097c30dfe2e1784623e129c509619c1b9d'
                 '40a12a9a8932ec27004bcace6b7d2d9755401b3875783d16bec6038fa85abe84'
                 'aeee272443a7327325a940479902c0cde7b050aa981cf44a016b770d55a35a2d'
                 'e414695a5e08507309a6bd61e69839523b37a74672b0f8d774fe8fae5896f30b')
sha256sums_x86_64=('6e187bb230525f1b85a950a3c72afdca201d7957797ec81d620bdaae9e810250'
                   '1c178265ae2a452698e358a4c0ebb4ae86f79edf49d0303d40bef6f539281c19'
                   'e203ff06381b0ba728b07181ebfa3b41bf18d5cae5857e5ffd462aece32df1fe'
                   'ba0cd36c4d69519e01feef5a391f9d59c8865713fe1a8462ad0bd77f04de09ec')

prepare() {
  mkdir eclipse-common

  for _pkg in ${pkgname[@]}; do
    [[ $_pkg == 'eclipse-common' ]] && continue

    if [[ $CARCH == x86_64 ]]; then
      bsdtar -xf "$_pkg-$_release-linux-gtk-x86_64.tar.gz"
    else
      bsdtar -xf "$_pkg-$_release-linux-gtk.tar.gz"
    fi

    mv eclipse $_pkg
  done
}

build() {
  ./commonify ${pkgname[@]}

  mkdir -p eclipse-common/dropins
  touch eclipse-common/dropins/.keep
}

package_eclipse-common() {
  pkgdesc+=" (common files)"
  depends=("java-environment>=8" webkit2gtk unzip)
  install=eclipse.install

  install -d "$pkgdir/usr/lib"
  cp -a eclipse-common "$pkgdir/usr/lib/eclipse"

  install -D eclipse.sh "$pkgdir/usr/bin/eclipse"
  install -Dm644 eclipse.desktop "$pkgdir/usr/share/applications/eclipse.desktop"

  for _i in 16 32 48 256; do
    install -Dm644 eclipse-common/plugins/org.eclipse.platform_*/eclipse${_i}.png \
      "$pkgdir/usr/share/icons/hicolor/${_i}x${_i}/apps/eclipse.png"
  done
}

_package() {
  _variant=${1#eclipse-}
  pkgdesc+=" (${_variant^^} variant)"
  depends=(eclipse-common)
  provides=("eclipse=$pkgver-$pkgrel")
  conflicts=(eclipse)

  case $_variant in
    java) replaces=(eclipse) ;;
    cpp)  replaces=(eclipse-cdt) ;;
  esac

  install -d "$pkgdir/usr/lib"
  cp -a $1 "$pkgdir/usr/lib/eclipse"
}
