# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Denis Martinez <deuns.martinez AT gmail.com>

pkgname=intel-tbb
pkgver=4.4_20160128
_tag=tbb${pkgver/\./}oss
_file=${_tag}_src_0.tgz
pkgrel=1
pkgdesc='High level abstract threading library'
arch=(i686 x86_64)
url='http://www.threadingbuildingblocks.org/'
license=(GPL)
depends=(gcc-libs)
source=(http://threadingbuildingblocks.org/sites/default/files/software_releases/source/$_file)
sha1sums=('80707e277f69d9b20eeebdd7a5f5331137868ce1')

build() {
  cd $_tag
  make
}

package() {
  cd $_tag
  install -d "$pkgdir"/usr/lib
  install -m755 build/linux_*/*.so* "$pkgdir"/usr/lib
  install -d "$pkgdir"/usr/include
  cp -a include/tbb "$pkgdir"/usr/include
}
