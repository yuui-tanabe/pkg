# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Denis Martinez <deuns.martinez AT gmail.com>

pkgname=intel-tbb
pkgver=2017_20160722
_tag=tbb${pkgver/\./}oss
_file=${_tag}_src.tgz
pkgrel=1
pkgdesc='High level abstract threading library'
arch=(i686 x86_64)
url='http://www.threadingbuildingblocks.org/'
license=(GPL)
depends=(gcc-libs)
source=(http://threadingbuildingblocks.org/sites/default/files/software_releases/source/$_file)
sha1sums=('872e5bec729b538e443dc1eccc1d31a084690793')

build() {
  cd $_tag
  export CXXFLAGS+=" -fno-lifetime-dse" # FS#49898
  make
}

package() {
  cd $_tag
  install -d "$pkgdir"/usr/lib
  install -m755 build/linux_*/*.so* "$pkgdir"/usr/lib
  install -d "$pkgdir"/usr/include
  cp -a include/tbb "$pkgdir"/usr/include
}
