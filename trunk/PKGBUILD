# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kde-l10n
pkgver=16.04.3
pkgrel=1
arch=('any')
url='http://www.kde.org/'
license=('GPL')
makedepends=('extra-cmake-modules' 'kdoctools' 'kdelibs' 'automoc4' 'kconfig' 'ki18n' 'qt5-tools' 'python')
options=('docs')

_languages=(
  'ar     "Arabic"'
  'ast    "Asturian"'
  'bg     "Bulgarian"'
  'bs     "Bosnian"'
  'ca     "Catalan"'
  'ca@valencia     "Catalan (Valencian)"'
  'cs     "Czech"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en_GB  "British English"'
  'eo     "Esperanto"'
  'es     "Spanish"'
  'et     "Estonian"'
  'eu     "Basque"'
  'fa     "Farsi"'
  'fi     "Finnish"'
  'fr     "French"'
  'ga     "Irish"'
  'gl     "Galician"'
  'he     "Hebrew"'
  'hi     "Hindi"'
  'hr     "Croatian"'
  'hu     "Hungarian"'
  'ia     "Interlingua"'
  'id     "Indonesian"'
  'is     "Icelandic"'
  'it     "Italian"'
  'ja     "Japanese"'
  'kk     "Kazakh"'
  'km     "Khmer"'
  'ko     "Korean"'
  'lt     "Lithuanian"'
  'lv     "Latvian"'
  'mr     "Marathi"'
  'nb     "Norwegian Bokmal"'
  'nds    "Low Saxon"'
  'nl     "Dutch"'
  'nn     "Norwegian Nynorsk"'
  'pa     "Panjabi/Punjabi"'
  'pl     "Polish"'
  'pt     "Portuguese"'
  'pt_BR  "Brazilian Portuguese"'
  'ro     "Romanian"'
  'ru     "Russian"'
  'sk     "Slovak"'
  'sl     "Slovenian"'
  'sr     "Serbian"'
  'sv     "Swedish"'
  'tr     "Turkish"'
  'ug     "Uyghur"'
  'uk     "Ukrainian"'
  'wa     "Walloon"'
  'zh_CN  "Chinese Simplified"'
  'zh_TW  "Chinese Traditional"'
)

pkgname=()
source=()

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=kde-l10n-${_locale,,}
  
  pkgname+=($_pkgname)
  source+=("http://download.kde.org/stable/applications/$pkgver/src/$pkgbase/$pkgbase-$_locale-$pkgver.tar.xz")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

md5sums=('aee1398dec0b1fca101d05611c690dbd'
         '5df9b24b37124c797fb008bc41af4128'
         '542fb0154069c77fac4829b6af9d90f6'
         'd1c842848b1b3007d98715e256ec35ae'
         'c3e302608bc7aadf0bf672422359c74b'
         '2c6e885d8570f5700eb0499ab49f198c'
         'fe2f97cea9bd82a7e1a04d964ef25ba7'
         'e46189c14f4a5e9a9bd7fe1a9e34e2ac'
         'bd43b4e5cae02cf2439da62be4e4d646'
         '1262bd08ae456ff65f47a6dffb238a8e'
         '0d011c09edcd8815e02becc011303500'
         '74ede69039275d47109b02e4117d7598'
         'e52bce6ea03c892cf7b76b98eb42ff49'
         '063f4fec3fb383b227c52710cc1bff1a'
         '7af3e82e1da4f453e19b89b731c79fcb'
         '0fc9c4f7ecef9e8500f54396af1296b4'
         '69ac7c5a4e716a26b9aae0d0bf326b90'
         '193e1116cc700d1a46eff3231d290fcc'
         '7758ce99825223fa4c52c20a11560f4b'
         'a90546542659a280ff0f8e21c2d6bea0'
         '884fdee7e203e22b3ad0a909de4d128c'
         '650c1921500e757f8e274329d1e2b2fa'
         '63c17676af7d6f353c5a10cc645cbd36'
         'd2805b6a04dbd627c95e3e8f88924609'
         '35e1452b8f821031843097d704fbc257'
         '90afca22c4c4b5fbee7fc10f266ce38c'
         'dba725f06e968fc8457fda145880a7f9'
         'd4b2b2b82728ff70e7ed6367af036c85'
         '57ff56060452d3be124e9ab769e2a708'
         'b3a7fa4fceb0637f8e04e684dc02790d'
         '53be1a81902486f870c50aec2522e0c6'
         'e93f10435a9688b36bf2313ea1769fa6'
         '13ba1e3b668322ff7c4f1e53d0322719'
         '273d10d15fe4a94824ae43b02d5a025b'
         '2fc0b417ab261bc7452d14fcaee72331'
         '3f0dc698f3ec91f133c1db17760a0480'
         'c2269a68bc9bd0d4902156784ab008da'
         '93ae819f0990c9d2c3815a3566f3ebfc'
         'd9be3fe497b951faf7ec558b4ecd2104'
         '97362e17584278a356b4e1f17411f8fb'
         'ec90c889549f57aa03aa105b99b407ec'
         '237df945b6923912e6cd34a4c868a046'
         '645a250a0e50489c1be1ff9cfd119ab6'
         'b5a3a50ffa6e02267a158fdaaa63ec26'
         'e128e0a3e09326931c4f06ffdb2cdeb2'
         'b01d956de316ab7d27eae12ef928e7e6'
         'b9c85a546c060d4608c10a89cf460734'
         '6db45d705beacbcee5aab4e96f9bc907'
         '32bd10abf8e7d7080fa7f10f41843dee'
         '3667220836a938132d94e12e157f1188'
         '23d7e11323221f2b42712489ecb45589'
         '8e0bbf31003a752a9db137f6a6492f08'
         '7a79e491965cbe2b707f916d3e00a394'
         'eebce77ba3d22b0c9d7048b6385fd946'
         '8dc83a08bb93690d76e560968ea2d4f3')

prepare() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}

# Remove kde-runtime and kde-workspace docs, conflicts with plasma-desktop
  sed -e '/kde-runtime/d' -e '/kde-workspace/d' -e '/kdeplasma-addons/d' \
   -i $pkgbase-$_locale-$pkgver/4/$_locale/docs/CMakeLists.txt || true
# kde-l10n-sr needs special casing
  sed -e '/kde-runtime/d' -e '/kde-workspace/d' -e '/kdeplasma-addons/d' \
   -i $pkgbase-$_locale-$pkgver/4/$_locale/*/docs/CMakeLists.txt || true
  done
}

build() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}
    
    mkdir -p build/$_locale
    cd build/$_locale
    cmake $srcdir/$pkgbase-$_locale-$pkgver \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr
    make
    cd ../..
  done
}

_package() {
  pkgdesc="$2 localization for KDE Applications"
  cd build/$1
  make DESTDIR="$pkgdir" install
}
