# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kde-l10n
pkgver=16.08.1
pkgrel=2
arch=('any')
url='http://www.kde.org/'
license=('GPL')
depends=('qt5-translations') # needed for RTL languages FS#50780
makedepends=('extra-cmake-modules' 'kdoctools' 'kdelibs' 'automoc4' 'kconfig' 'ki18n' 'qt5-tools' 'python')
options=('docs')

_languages=(
  'ar     "Arabic"'
  'ast    "Asturian"'
  'bg     "Bulgarian"'
  'bs     "Bosnian"'
  'ca     "Catalan"'
  'ca@valencia     "Catalan (Valencian)"'
  'cs     "Czech"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en_GB  "British English"'
  'eo     "Esperanto"'
  'es     "Spanish"'
  'et     "Estonian"'
  'eu     "Basque"'
  'fa     "Farsi"'
  'fi     "Finnish"'
  'fr     "French"'
  'ga     "Irish"'
  'gl     "Galician"'
  'he     "Hebrew"'
  'hi     "Hindi"'
  'hr     "Croatian"'
  'hu     "Hungarian"'
  'ia     "Interlingua"'
  'id     "Indonesian"'
  'is     "Icelandic"'
  'it     "Italian"'
  'ja     "Japanese"'
  'kk     "Kazakh"'
  'km     "Khmer"'
  'ko     "Korean"'
  'lt     "Lithuanian"'
  'lv     "Latvian"'
  'mr     "Marathi"'
  'nb     "Norwegian Bokmal"'
  'nds    "Low Saxon"'
  'nl     "Dutch"'
  'nn     "Norwegian Nynorsk"'
  'pa     "Panjabi/Punjabi"'
  'pl     "Polish"'
  'pt     "Portuguese"'
  'pt_BR  "Brazilian Portuguese"'
  'ro     "Romanian"'
  'ru     "Russian"'
  'sk     "Slovak"'
  'sl     "Slovenian"'
  'sr     "Serbian"'
  'sv     "Swedish"'
  'tr     "Turkish"'
  'ug     "Uyghur"'
  'uk     "Ukrainian"'
  'wa     "Walloon"'
  'zh_CN  "Chinese Simplified"'
  'zh_TW  "Chinese Traditional"'
)

pkgname=()
source=()

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=kde-l10n-${_locale,,}
  
  pkgname+=($_pkgname)
  source+=("http://download.kde.org/stable/applications/$pkgver/src/$pkgbase/$pkgbase-$_locale-$pkgver.tar.xz")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

md5sums=('f275e1fe8511280c85800acb9a25da40'
         'ecb10f130d3004d6651c664a6b31b7a2'
         '2d6e215e2e5a8b4f9e7ad7880dbb3938'
         'd0eb452ed06beac4f1b39f38c9ddcfa8'
         '30c392e5fe17dd76bf48688f54b8fc7e'
         '189b1c5800773ade61c8422da39c63ec'
         'fce256f3c5671f4a8e0fe03e5ae5dedf'
         '3c69ffd4a710071f2d53bc9ca33480e2'
         'a4ef2f1b5936a6d4992c2fe09a2a40e2'
         'c9e2cd8aa88dafabfc1d84dbb323f3c0'
         '0c3d1cd7581fc3e7ac7f77d84ed69082'
         '985b679ae3b90e53db8ede41edc80225'
         '0d607188279e980579e502b43db93892'
         '9987b1017c7cb4c5b7baee5e48eca0df'
         '2a202e09209c61cf2f24ba44d9a7e5e7'
         'e0c12a0ac8282dcb89c7fbf0bd057240'
         'ebfe57bad4720f9afc454829de11b37d'
         '89ed9740b4f6238b1b7fd5568c8f0dbb'
         'c182159860d813d09013eb55b3e4610e'
         '44a1942827a8d1fbb3e0f26a2d4be503'
         '761120d9ed42551270675b8fe57980be'
         '88b0038a397e914daa1fef14b624c548'
         'f1f6ecbfa7355dc3b0faee52f44c8f48'
         'e292b69ecc551d92f2b754f9144b2b5a'
         'ff1e5cb78916f0d42d8b4e217197047b'
         '35bc54aa938556380a2204c91cdb65db'
         '3d979a70e8300a31ced9e72b268c9010'
         '3348f04f59fde4efe80cda4cd1bc253c'
         'c364d214ade85015cbb93ed09b287a4c'
         'b764f01cfa6f309d7273fa3f6324f954'
         '044b30b16431ac98a2b2927361420440'
         '30ca7327b2eb68e794fb6297fba431f2'
         'e9dfbcba3267e7fc2e7bdf95ef0c02fd'
         '0129e8e75e4bc87e13b98ccf66e7d321'
         'b4097721bcf629cf1aaa96ac116a19c0'
         '286fba267f1cf3f6037df508b7f17a9d'
         '12b5868664f84e273853a4677eb64f2a'
         '82291e8e6d8333a34e365e2ed3872bab'
         '71f74f68bdfbbf398e82d0ad4fa8d94d'
         '71aabe78561e7902d645a5c64e9a26db'
         'c86e565d263a84f706e1eb25c2bf1341'
         '230849c8b0379dde60d8209abf855c80'
         '8b4bb84a88a42e733b3d431dafe9e8d8'
         '27feed0c2e30462bdc0c63009837ce92'
         'cb442d73d85bce9820c77e835a400c4e'
         '8f3823166de6462811422d4df98c1f52'
         'e34e9594610356071b1d81b7fafeaec6'
         'ad9cd427c51de236771beb82c4be6944'
         '4ce22c55bcb10b14cbf856f81ba4637b'
         'ce03187f766316b73f6dcaaba68aadd4'
         '9f1cc53008b3e098ed89af2e83f3aced'
         '4a276cf3450a703990d71b2c50bacf8f'
         'd44fc611dbee0b73b6631b980e25621b'
         '60580c39c0de60129073803ab1ae44f0'
         'e1a783aa4271db03d8eca3e223e86166')

prepare() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}

# Remove kde-runtime and kde-workspace docs, conflicts with plasma-desktop
  sed -e '/kde-runtime/d' -e '/kde-workspace/d' -e '/kdeplasma-addons/d' \
   -i $pkgbase-$_locale-$pkgver/4/$_locale/docs/CMakeLists.txt || true
# kde-l10n-sr needs special casing
  sed -e '/kde-runtime/d' -e '/kde-workspace/d' -e '/kdeplasma-addons/d' \
   -i $pkgbase-$_locale-$pkgver/4/$_locale/*/docs/CMakeLists.txt || true
  done
}

build() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}
    
    mkdir -p build/$_locale
    cd build/$_locale
    cmake $srcdir/$pkgbase-$_locale-$pkgver \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr
    make
    cd ../..
  done
}

_package() {
  pkgdesc="$2 localization for KDE Applications"
  cd build/$1
  make DESTDIR="$pkgdir" install
}
