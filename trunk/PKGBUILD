# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=scim-hangul
pkgver=0.4.0
pkgrel=3
pkgdesc='Korean input method module for SCIM'
url='https://github.com/choehwanjin/scim-hangul'
license=('GPL')
arch=('i686' 'x86_64')
depends=('libhangul' 'scim')
source=("https://github.com/choehwanjin/scim-hangul/archive/v${pkgver}.tar.gz"
        '2012.patch')
sha256sums=('7f116c87e78da51363fb4968d627364718de6ec02aa6c6119451fdf403e668c0'
            'dbb0358ef40c9ccb938571304e9f0844a67b4aeb5f1c8995ea62b2ae54aae9cc')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -p1 -i ../2012.patch
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr --enable-skim-support
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
