# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=fonts-tlwg
pkgver=0.6.4
pkgrel=1
pkgdesc='Collection of scalable Thai fonts'
url='https://linux.thai.net/projects/fonts-tlwg'
arch=('any')
license=('GPL' 'custom')
makedepends=('fontforge')
depends=('xorg-fonts-encodings' 'xorg-fonts-alias' 'xorg-font-utils' 'fontconfig')
source=("https://linux.thai.net/pub/thailinux/software/${pkgname}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('e008257c8e0b95dbd37e43d5a62a43ea1b33b5dc')

conflicts=('ttf-tlwg')
provides=('ttf-tlwg')
replaces=('ttf-tlwg')

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-ttfdir=/usr/share/fonts/TTF \

  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
