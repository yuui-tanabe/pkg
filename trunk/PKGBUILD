# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kde-l10n
pkgver=15.04.3
pkgrel=1
arch=('any')
url='http://www.kde.org/'
license=('GPL')
makedepends=('extra-cmake-modules' 'kdoctools' 'kdelibs' 'automoc4' 'kconfig' 'ki18n' 'qt5-tools')
options=('docs')

_languages=(
  'ar     "Arabic"'
  'bg     "Bulgarian"'
  'bs     "Bosnian"'
  'ca     "Catalan"'
  'ca@valencia     "Catalan (Valencian)"'
  'cs     "Czech"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en_GB  "British English"'
  'eo     "Esperanto"'
  'es     "Spanish"'
  'et     "Estonian"'
  'eu     "Basque"'
  'fa     "Farsi"'
  'fi     "Finnish"'
  'fr     "French"'
  'ga     "Irish"'
  'gl     "Galician"'
  'he     "Hebrew"'
  'hi     "Hindi"'
  'hr     "Croatian"'
  'hu     "Hungarian"'
  'ia     "Interlingua"'
  'id     "Indonesian"'
  'is     "Icelandic"'
  'it     "Italian"'
  'ja     "Japanese"'
  'kk     "Kazakh"'
  'km     "Khmer"'
  'ko     "Korean"'
  'lt     "Lithuanian"'
  'lv     "Latvian"'
  'mr     "Marathi"'
  'nb     "Norwegian Bokmal"'
  'nds    "Low Saxon"'
  'nl     "Dutch"'
  'nn     "Norwegian Nynorsk"'
  'pa     "Panjabi/Punjabi"'
  'pl     "Polish"'
  'pt     "Portuguese"'
  'pt_BR  "Brazilian Portuguese"'
  'ro     "Romanian"'
  'ru     "Russian"'
  'sk     "Slovak"'
  'sl     "Slovenian"'
  'sr     "Serbian"'
  'sv     "Swedish"'
  'tr     "Turkish"'
  'ug     "Uyghur"'
  'uk     "Ukrainian"'
  'wa     "Walloon"'
  'zh_CN  "Chinese Simplified"'
  'zh_TW  "Chinese Traditional"'
)

pkgname=()
source=()

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=kde-l10n-${_locale,,}
  
  pkgname+=($_pkgname)
  source+=("http://download.kde.org/stable/applications/$pkgver/src/$pkgbase/$pkgbase-$_locale-$pkgver.tar.xz")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

md5sums=('7d227fc39f7eca96273a68af91f79c7c'
         '68a33223dadc156f2a8d208e80bb7671'
         'fcf59f3c72ae54769a5bf1861435f905'
         'e53ebb44c8aeddd745b01586d51e3c66'
         'ca70e29fced219a26508eaca1a010b7d'
         '599a623779e83adbb87b4d30a2f504c4'
         'd1f538f8b3dcb264236f7adae58d2ee5'
         '782641163080a797feb5f4049a3bbb1d'
         '3bc0324989b7222d4a64fd3169bb5f3d'
         'd8c9f38c5e9756a424780ac62e23165e'
         '403aabce1f34629a58c07b68454c1f92'
         '756d9bb7280397ced2e4cc2db5e8b133'
         '58ab7d91af9fe19638c0c0edf166293b'
         '1e7c1d4ba46c12e508992a12bc190395'
         'a3835650ddb4baa517ad702fb9a3f5ba'
         'd6a6c0b43843b137a847c1b027339108'
         '27efd770cb6209c3bc6b161c050c9a2d'
         '738ae3709b94a113e61ef742727b6536'
         'b2a78cecf20b733330fb60f96e61dcf7'
         '567fff9cc456916033ed6e1376561531'
         '1f80da071ed0611fdd7124f2dae9f371'
         '77699bdf4ee4c1e150c568fe268a5608'
         '6656264cd2e1b0cb1a63eff43783caa2'
         'd1d7b7a7ed17dd7eda4776da6638652e'
         'e47d85bdc411eecafa6fb3c64e7558cc'
         '99496729f455f632dec0737990bf1795'
         '3582a10e13e99af590af48a7ae9fb3af'
         'b6c030a666b8d261bcd0167a4f38a886'
         'b9b597a2233069d683aa3003d31137cb'
         '54c3e84f0ce26387587e496a4c998300'
         '8698c78970c1299c79f67c1b077ac260'
         'ce87033b43e48ef44292802c84d4aaab'
         'f37987520cfa424ade17642e243f8c5e'
         'ede7500605dd06f52e6b9fae418cc7df'
         '729327e36d84cba69506a4e244fee1b6'
         '7d553f29a81fafe0865a6604dc7284ea'
         'c29fd1262ba68e4148d0d68c6f94ffa1'
         '6c0e9f3283074736a4b8df30dd6c4918'
         '4f82a2676b729144b003115af036ada3'
         '0b9f9df1c874e9ae30d25629f832082c'
         '44dc075f683f20d3cbfb810cabe0639e'
         '7320f40f8168c4d8528f567533580c1e'
         '2b8798843ef6e967d9ecb647d8ba8a71'
         '2075b50e6b8c439b8b8e65130bf42626'
         '86a58415ddadb98ec1446577c7a3d44d'
         '38639643f18caae4a11941ae6eed1288'
         '1787ba49d851a0938701ba7efb8bdaef'
         'b7a551eda55c830db716687bcc3948ba'
         'c55f8b3c4d0c56ff14c30cb4e6fcf12c'
         'b1ba5582175850d8f7064e5771b5e467'
         '451579f9b82cc68c4f807dd1a65ceb59'
         '090983f8d448dca51921e114b53cc355'
         '18c1d963417e054b508532b0c0347855'
         '13360f76d5a55b2a23035ee194e08eae')

prepare() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}

# Remove kde-runtime and kde-workspace docs, conflicts with plasma-desktop
  sed -e '/kde-runtime/d' -e '/kde-workspace/d' -i $pkgbase-$_locale-$pkgver/4/$_locale/docs/CMakeLists.txt || true

  done
}

build() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}
    
    mkdir -p build/$_locale
    cd build/$_locale
    cmake $srcdir/$pkgbase-$_locale-$pkgver \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr
    make
    cd ../..
  done
}

_package() {
  pkgdesc="$2 localization for KDE"
  cd build/$1
  make DESTDIR="$pkgdir" install
}

