# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Marios Abatis <marios.archlinux ατ gmail δοτ com>

pkgname=chemical-mime-data
pkgver=0.1.94
pkgrel=8
pkgdesc="Collection of data files to add support for various chemical MIME types"
arch=('any')
url="https://chemical-mime.sourceforge.net/"
license=('LGPL')
depends=('xdg-utils' 'gnome-mime-data' 'hicolor-icon-theme')
makedepends=('intltool' 'libxslt' 'imagemagick' 'librsvg')
source=("https://downloads.sourceforge.net/chemical-mime/${pkgname}-${pkgver}.tar.gz")
sha256sums=('d0f0cac5d70716ba626c222de7cf43b6d80bdf369e6ce8bafac433c1b3ec9ca2')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --disable-update-database
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
