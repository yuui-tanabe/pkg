# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pkgname=ktnef
pkgname=lib$_pkgname
pkgver=16.08.3
pkgrel=1
pkgdesc="API for handling TNEF data"
arch=(i686 x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(kcalutils kcontacts)
makedepends=(extra-cmake-modules python kdoctools kdesignerplugin)
source=("http://download.kde.org/stable/applications/$pkgver/src/$_pkgname-$pkgver.tar.xz")
md5sums=('5646111e12e3539232f0768c2cd578ca')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$_pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
