# $Id$
# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=go
epoch=2
pkgver=1.8
pkgrel=2
pkgdesc='Core compiler tools for the Go programming language'
arch=(i686 x86_64)
url='http://golang.org/'
license=(BSD)
makedepends=(git go)
options=(!strip staticlibs)
_commit=cd6b6202dd1559b3ac63179b45f1833fcfbe7eca # go1.8
source=(git+https://go.googlesource.com/go#commit=$_commit
        0001-time-make-the-ParseInLocation-test-more-robust.patch)
md5sums=('SKIP'
         'a9031d2fdd5d0951980db7ffe20afe41')

export GOOS=linux
case "$CARCH" in
  x86_64) export GOARCH=amd64 ;;
  i686) export GOARCH=386 GO386=387 ;;
esac
export GOROOT_FINAL=/usr/lib/go
export GOROOT_BOOTSTRAP=/usr/lib/go

prepare() {
  cd $pkgname

  # fixes time/format test with tzdata >=2017a
  patch -p1 -i "$srcdir/0001-time-make-the-ParseInLocation-test-more-robust.patch"
}

build() {
  export GOROOT="$srcdir/$pkgname"
  export GOBIN="$GOROOT/bin"
  export GOPATH="$srcdir/"

  cd $pkgname/src
  ./make.bash --no-clean
}

check() {
  export GOROOT="$srcdir/$pkgname"
  export GOBIN="$GOROOT/bin"
  export PATH="$srcdir/$pkgname-$pkgver/bin:$PATH"
  export GO_TEST_TIMEOUT_SCALE=2

  # The cgo_test and race tests fail via run.bash but not if run manually.
  # Assume that five "failed" messages are okay and just re-run failed tests.
  cd $pkgname/src
  ./run.bash --no-rebuild -v -v -v -k |& tee run.log
  if (( $(grep -c Failed: run.log) -gt 5 )) && grep -q FAILED run.log; then
    return 1
  fi

  go tool dist test -v -v -v -run=^cgo_test$
  go tool dist test -v -v -v -run=^race$
}

package() {
  cd $pkgbase

  install -d "$pkgdir/usr/bin" "$pkgdir/usr/lib/go" "$pkgdir/usr/share/doc/go"
  cp -a bin pkg src lib "$pkgdir/usr/lib/go"
  cp -r doc/* "$pkgdir/usr/share/doc/go"

  ln -sf /usr/lib/go/bin/go "$pkgdir/usr/bin/go"
  ln -sf /usr/lib/go/bin/gofmt "$pkgdir/usr/bin/gofmt"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/go/LICENSE"
  install -Dm644 VERSION "$pkgdir/usr/lib/go/VERSION"

  rm -rf "$pkgdir/usr/lib/go/pkg/bootstrap"
  rm -rf "$pkgdir/usr/lib/go/pkg/tool/*/api"
}
