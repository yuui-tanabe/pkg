# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgbase=mutagen
pkgname=('mutagen' 'python-mutagen')
pkgver=1.39
pkgrel=1
arch=('any')
url="https://github.com/quodlibet/mutagen"
license=('GPL2')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python2-pytest' 'python-hypothesis' 'python2-hypothesis' 'python-pyflakes' 'python2-pyflakes'
              'python-pycodestyle' 'python2-pycodestyle')
source=($pkgbase-$pkgver.tar.gz::"https://github.com/quodlibet/mutagen/releases/download/release-$pkgver/$pkgbase-$pkgver.tar.gz"
        $pkgbase-$pkgver.tar.gz.sig::"https://github.com/quodlibet/mutagen/releases/download/release-$pkgver/$pkgbase-$pkgver.tar.gz.sig")
sha256sums=('9da92566458ffe5618ffd26167abaa8fd81f02a7397b8734ec14dfe14e8a19e4'
            'SKIP')
validpgpkeys=('0EBF782C5D53F7E5FB02A66746BD761F7A49B0EC')

prepare() {
  cp -r ${pkgbase}-${pkgver} python-${pkgbase}-${pkgver}
}

check() {
  cd ${pkgbase}-${pkgver}
  LANG=en_US.UTF8 python2 setup.py test
  cd ../python-${pkgbase}-${pkgver}
  LANG=en_US.UTF8 python setup.py test
}

package_mutagen() {
  pkgdesc="An audio metadata tag reader and writer"
  depends=('python2')
 
  cd ${pkgbase}-${pkgver}
  python2 setup.py install --root="${pkgdir}"
}

package_python-mutagen() {
  pkgdesc="An audio metadata tag reader and writer library for Python 3"
  depends=('python')

  cd python-${pkgbase}-${pkgver}
  python setup.py install --root="${pkgdir}"
  rm -r "${pkgdir}"/usr/{bin,share}
}
