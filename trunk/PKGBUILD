# $Id$
# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=amarok
pkgver=2.8.0
pkgrel=19
pkgdesc="The powerful music player for KDE"
arch=(x86_64)
url="http://amarok.kde.org/"
license=('GPL2' 'LGPL2.1' 'FDL')
depends=('kdebase-runtime' 'mariadb' 'taglib-extras'
        'liblastfm' 'ffmpeg' 'libofa' 'qjson' 'qtscriptgenerator')
makedepends=('automoc4' 'cmake' 'libgpod' 'libmtp' 'loudmouth' 'libmygpo-qt' 'qca-qt4')
optdepends=('libgpod: support for Apple iPod audio devices'
            'libmtp: support for portable media devices'
            'loudmouth: backend needed by mp3tunes for syncing'
            'ifuse: support for Apple iPod Touch and iPhone'
            'libmygpo-qt: gpodder.net Internet Service'
            'gst-libav: MP3 codecs')
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2"
        taglib_version_detect.patch::"https://cgit.kde.org/amarok.git/patch/?id=fec8817c" 
        amarok-ffmpeg3.patch::"https://git.reviewboard.kde.org/r/126682/diff/raw/"
        amarok-no-qtwebkit.patch::"https://cgit.kde.org/amarok.git/patch/?id=d65ce101"
        amarok-plasma5.7.patch::"https://cgit.kde.org/amarok.git/patch/?id=9449ec56")
sha256sums=('f4b89b28f217a6d947e55fb0890de22fad8c404794c6beeb2dcaf2711b4f8ec6'
            'bd7034e519999b6522e72e3961ca758630ad9b4db88557f795cf08956ea3dfee'
            'fbb18f1ff234716f6f8d3afd710b50f337453e536eb8288c3a9c4613edd6570f'
            '6880a314a437100a3496540f2f9a0f2d14a4ff61deeeee07a42ba3f4beb1432b'
            'acc0fa104c99be046fa07b01a142f77c323b99943833d2e2139bfdcbec39e4a5')

prepare() {
  mkdir build

  cd ${pkgname}-${pkgver}
  patch -Np1 -i ../taglib_version_detect.patch
# Fix build against ffmpeg 3.0
  patch -p1 -i ../amarok-ffmpeg3.patch
# Disable applets that require qtwebkit
  patch -p1 -i ../amarok-no-qtwebkit.patch
# Fix tooltips MPRIS controls in Plasma 5.7
  patch -p1 -i ../amarok-plasma5.7.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_NepomukCore=OFF \
    -DWITH_Soprano=OFF \
    -DWITH_QTWEBKIT=OFF
  make
}

package(){
  cd build
  make DESTDIR="${pkgdir}" install
}
