# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=bijiben
pkgver=3.20.2
pkgrel=1
pkgdesc="Write out notes, every detail matters"
url="https://wiki.gnome.org/Apps/Bijiben"
arch=(i686 x86_64)
license=(GPL)
depends=(tracker zeitgeist webkitgtk gnome-online-accounts evolution-data-server)
makedepends=(intltool itstool evolution python gnome-common appstream-glib git)
options=(!emptydirs)
groups=(gnome-extra)
_commit=774ebde9425e77deb10f75b8f05138736d0655d6  # master
source=("git://git.gnome.org/bijiben#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^BIJIBEN_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
  git tag -f 3.21.2 baab416de17927a3658b889ce611b80cc8d4a835
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname --disable-update-mimedb
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
