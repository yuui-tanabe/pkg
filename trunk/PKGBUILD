# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=xournal
pkgver=0.4.8
pkgrel=2
pkgdesc='Notetaking and sketching application'
url='http://xournal.sourceforge.net/'
license=('GPL')
arch=('i686' 'x86_64')
optdepends=('ghostscript: import PS/PDF files as bitmap backgrounds')
depends=('libgnomecanvas' 'poppler-glib' 'shared-mime-info' 'desktop-file-utils' 'hicolor-icon-theme')
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('233887a38136452dcb4652c35d08366fc7355f57ed46753db83e3e0f3193ef30')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install desktop-install
}
