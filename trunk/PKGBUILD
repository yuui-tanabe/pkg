# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>

pkgbase=vulkan-loader-layers
pkgname=(vulkan-icd-loader vulkan-validation-layers)
_pkgname=Vulkan-LoaderAndValidationLayers
pkgver=1.0.21.0
pkgrel=1
arch=(i686 x86_64)
url="https://www.khronos.org/vulkan/"
license=('custom')
depends=()
makedepends=(git cmake python-lxml libx11 wayland)
source=("https://github.com/KhronosGroup/${_pkgname}/archive/sdk-${pkgver}.tar.gz"
        'git://github.com/KhronosGroup/glslang.git#commit=e4821e43c86d97bcf65fb07c1f70471b7102978d'
        'git://github.com/KhronosGroup/SPIRV-Tools.git#commit=1a9385bbd0e6eae188c14302cf37c415ecc8b698'
        'git://github.com/KhronosGroup/SPIRV-Headers')
sha256sums=('205e6feee6af29d0d23d241c666261e9646e595689e9a90900fbc63c4c838134'
            'SKIP'
            'SKIP'
            'SKIP')

build() {
  cd "${srcdir}/${_pkgname}-sdk-${pkgver}"

  pushd ${srcdir}/glslang
  # Revert glslang a5c33d6ffb34ccede5b233bc724c907166b6e479
  # See https://github.com/KhronosGroup/Vulkan-LoaderAndValidationLayers/issues/681
  git diff-index --quiet HEAD | true
  rc=${PIPESTATUS[0]}
  if (( $rc == 0 ))
  then
    echo "applying patch to revert glslang a5c33d"
    git apply "${srcdir}/${_pkgname}-sdk-${pkgver}"/glslang_revert_a5c33d.patch.txt
  fi
  popd
  
  # reproduce some of the update_external_sources.sh script
  rm -rf external ; mkdir external ; cd external
  git clone ${srcdir}/glslang
  git clone ${srcdir}/SPIRV-Tools spirv-tools
  pushd spirv-tools
  git clone ${srcdir}/SPIRV-Headers external/spirv-headers
  popd
  
  pushd glslang
  rm -rf build ; mkdir build ; cd build
  cmake -D CMAKE_BUILD_TYPE=Release ..
  cmake -D CMAKE_BUILD_TYPE=Release ..
  make
  make install
  popd
  
  pushd spirv-tools
  rm -rf build ; mkdir build ; cd build
  cmake -D CMAKE_BUILD_TYPE=Release ..
  make
  popd
  
  cd "${srcdir}/${_pkgname}-sdk-${pkgver}"

  rm -rf build ; mkdir build ; cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_DATADIR=/share \
    -DCMAKE_SKIP_RPATH=True \
    -DBUILD_TESTS=Off \
    -DBUILD_WSI_XLIB_SUPPORT=On \
    -DBUILD_WSI_WAYLAND_SUPPORT=On \
    -DCMAKE_BUILD_TYPE=Release \
    ..
  make
  make DESTDIR=install install
}

package_vulkan-icd-loader() {
  pkgdesc="Vulkan Installable Client Driver (ICD) Loader"
  depends=(glibc)
  cd "${srcdir}/${_pkgname}-sdk-${pkgver}"
  
  install -dm755 ${pkgdir}/usr/{lib,include/vulkan}
  install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}
  install -dm755 ${pkgdir}/etc/vulkan/icd.d/
  
  cp -rv  build/loader/libvulkan.so* ${pkgdir}/usr/lib
  install -m644 include/vulkan/vk_icd.h ${pkgdir}/usr/include/vulkan/
  
  install -m644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/
}

package_vulkan-validation-layers() {
  pkgdesc="Vulkan Software Development Kit"
  depends=(gcc-libs vulkan-icd-loader vulkan-headers)
  conflicts=('vulkan-sdk')
  replaces=('vulkan-sdk')
  groups=('vulkan-devel')
  cd "${srcdir}/${_pkgname}-sdk-${pkgver}"
  
  install -dm755 ${pkgdir}/usr/{bin,lib,include/vulkan}
  install -dm755 ${pkgdir}/usr/share/{doc,licenses}/${pkgname}
  install -dm755 ${pkgdir}/usr/share/vulkan/{explicit,implicit}_layer.d

  install -m755 build/demos/vulkaninfo ${pkgdir}/usr/bin/
  
  install -m644 build/layers/*.so ${pkgdir}/usr/lib

  install -m644 include/vulkan/* ${pkgdir}/usr/include/vulkan/
  # already in vulkan-headers
  rm ${pkgdir}/usr/include/vulkan/{vulkan,vk_platform}.h
  # already in vulkan-icd-loader
  rm ${pkgdir}/usr/include/vulkan/vk_icd.h

  # fix library paths  
  for i in layers/linux/*.json ; do 
    sed 's@./@@' $i > ${pkgdir}/usr/share/vulkan/explicit_layer.d/$(basename $i)
  done
  
  install -m644 loader/LoaderAndLayerInterface.md "${pkgdir}"/usr/share/doc/${pkgname}/

  install -m644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/
}
