# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=physfs
pkgver=3.0.0
pkgrel=2
pkgdesc="A library to provide abstract access to various archives"
arch=('i686' 'x86_64')
url="http://icculus.org/physfs/"
license=('ZLIB')
depends=('zlib')
makedepends=('cmake' 'doxygen')
source=(http://icculus.org/physfs/downloads/${pkgname}-${pkgver}.tar.bz2)
sha1sums=('31ffa3625061fa41dc5e79951eb4a61c0a8b356d')

build() {
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPHYSFS_BUILD_TEST=OFF
  make all docs
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
  install -d "${pkgdir}"/usr/share/{doc/physfs,man/man3}
  install -m644 docs/html/* "${pkgdir}/usr/share/doc/physfs"
  install -m644 docs/man/man3/* "${pkgdir}/usr/share/man/man3"

  for i in author Deinit description extension Free Init major Malloc minor opaque patch Realloc remove url ; do
    mv "${pkgdir}/usr/share/man/man3/$i.3" "${pkgdir}/usr/share/man/man3/PHYSFS_$i.3"
  done

  install -D -m644 ../${pkgname}-${pkgver}/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
