# $Id$
# Maintainer:
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Denis Martinez <deuns.martinez AT gmail.com>

pkgname=intel-tbb
pkgver=2018_20170919
_pkgver=2018_U1
pkgrel=1
pkgdesc='High level abstract threading library'
arch=(x86_64)
url='http://www.threadingbuildingblocks.org/'
license=(GPL)
depends=(gcc-libs)
source=(https://github.com/01org/tbb/archive/$_pkgver.tar.gz)
sha256sums=('c6462217d4ecef2b44fce63cfdf31f9db4f6ff493869899d497a5aef68b05fc5')

build() {
  cd tbb-$_pkgver
  export CXXFLAGS+=" -fno-lifetime-dse" # FS#49898
  make
}

package() {
  cd tbb-$_pkgver
  install -d "$pkgdir"/usr/lib
  install -m755 build/linux_*/*.so* "$pkgdir"/usr/lib
  install -d "$pkgdir"/usr/include
  cp -a include/tbb "$pkgdir"/usr/include
}
