# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kde-l10n
pkgver=16.04.1
pkgrel=1
arch=('any')
url='http://www.kde.org/'
license=('GPL')
makedepends=('extra-cmake-modules' 'kdoctools' 'kdelibs' 'automoc4' 'kconfig' 'ki18n' 'qt5-tools' 'python')
options=('docs')

_languages=(
  'ar     "Arabic"'
  'ast    "Asturian"'
  'bg     "Bulgarian"'
  'bs     "Bosnian"'
  'ca     "Catalan"'
  'ca@valencia     "Catalan (Valencian)"'
  'cs     "Czech"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en_GB  "British English"'
  'eo     "Esperanto"'
  'es     "Spanish"'
  'et     "Estonian"'
  'eu     "Basque"'
  'fa     "Farsi"'
  'fi     "Finnish"'
  'fr     "French"'
  'ga     "Irish"'
  'gl     "Galician"'
  'he     "Hebrew"'
  'hi     "Hindi"'
  'hr     "Croatian"'
  'hu     "Hungarian"'
  'ia     "Interlingua"'
  'id     "Indonesian"'
  'is     "Icelandic"'
  'it     "Italian"'
  'ja     "Japanese"'
  'kk     "Kazakh"'
  'km     "Khmer"'
  'ko     "Korean"'
  'lt     "Lithuanian"'
  'lv     "Latvian"'
  'mr     "Marathi"'
  'nb     "Norwegian Bokmal"'
  'nds    "Low Saxon"'
  'nl     "Dutch"'
  'nn     "Norwegian Nynorsk"'
  'pa     "Panjabi/Punjabi"'
  'pl     "Polish"'
  'pt     "Portuguese"'
  'pt_BR  "Brazilian Portuguese"'
  'ro     "Romanian"'
  'ru     "Russian"'
  'sk     "Slovak"'
  'sl     "Slovenian"'
  'sr     "Serbian"'
  'sv     "Swedish"'
  'tr     "Turkish"'
  'ug     "Uyghur"'
  'uk     "Ukrainian"'
  'wa     "Walloon"'
  'zh_CN  "Chinese Simplified"'
  'zh_TW  "Chinese Traditional"'
)

pkgname=()
source=()

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=kde-l10n-${_locale,,}
  
  pkgname+=($_pkgname)
  source+=("http://download.kde.org/stable/applications/$pkgver/src/$pkgbase/$pkgbase-$_locale-$pkgver.tar.xz")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

md5sums=('93b3bdbc0b63c87412f6edaf6c0e96bd'
         '8bbedc48c942aa473e312201281725c9'
         '471e7a781c1331a3f6de4459d0c6d525'
         'a94159f7c5b13fbca39dc38d21b83adb'
         'bcf4a7f418df03cb0a6522132c16c732'
         'a05d6872d6b6a1a80db5122bdf87a4f8'
         'ab0db6bf9931582dbef4dc2d9f690c40'
         'c2409fb1ba11378db0a660a3cce65929'
         'fc34eea65cc910e84ef46de4bcdda212'
         '2ba17a76f136eb17f8508d1cd2bbc675'
         'c4fa667999e85621af57f4b4d3d57902'
         '5f74e51b858c229c22027d1b8c7ac416'
         '5cf9cc283cfea10cbd1b89f0b7b3ee72'
         '3da616124f5868c5e0f4d9fad399ec74'
         '5257cd6e8f6cf4da4bf99ac7528e4434'
         '17485007282c2c827e950d237d40ec61'
         '7980d935eb42c0cc0a973f48c045fc95'
         '2ba8f5e0385cf0402d2d1bccb7dc974c'
         '0481f84f838bad18e78a6a30476ee509'
         '5003d31f2f762285b52bbb06f132c51b'
         'b0c67e9d23f599296a3e665ae8c439e5'
         'd029fba53d147393cc0534aeeef16124'
         '9f01626d1536c060fb6308ced78b1fa4'
         '4d73357883142c2683777afaf36b92ca'
         '643146ae581de59ab3e1085dbc1b8291'
         '174924ce4f8158e9a7d7df493048c9cf'
         '78e304e96f05ecd1559ae685a4d66bbe'
         '7e7d97be25e1c42eaf96a94e402a09dd'
         '41169d4d7fbcd72be1f559b9dedfb09e'
         '6a2c9baa1fe8901919518218f677e32c'
         '5d8eed71b3f01d3d7d51d572dc387b96'
         '989ae62609bbcff85c7f21b052399120'
         '98d801ec32340f0660cf822959063e1f'
         '74b4602c52d96f6e41cbf5a9bade9057'
         '4bb647e4f049a3c382e33d0121355b4f'
         '2c5182d6bb8212a943b93b1ff4179628'
         '2d1795e123487d48b3d89ef79e807aad'
         '6761272d724b8caa7498f3dbc4f6274d'
         '0510842dda9562c96ac4686c010e6760'
         '8491e5f47f95160f1066941636fe9fce'
         '6f9dcd5eade93b33aa9e240bee940450'
         '5c49ad6f6419cbb01f9c5f3a95e2ef16'
         'bf752cc0d9cc99f9a57541f048d92850'
         '75825522adc3e861e8c7059dfac9ea5d'
         '76a6f5f590dc199bf4cd34becc998752'
         'e36212643a76dd6fafd1dfa906dc51f6'
         'bc5cbeb8bd299f31c1e7445b7e13956d'
         '1ae011a72aba22839ac101e8df2f0586'
         'c0a592ded7f6779576516ae85062bf8b'
         'd0bdf09b833cc7f7f0b06748b07b1a09'
         'b5b0d46454491b66077da83235d1a0cc'
         'd5d40fbf3de3c123bef51ff55e720714'
         '7ea00c1f99071ec1e73b8a5a3bbed045'
         'bfbb00577a20ffa9c0272d712a0b8fb2'
         '2cb9d7591ed6e14e4a5e7ad466254435')

prepare() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}

# Remove kde-runtime and kde-workspace docs, conflicts with plasma-desktop
  sed -e '/kde-runtime/d' -e '/kde-workspace/d' -e '/kdeplasma-addons/d' \
   -i $pkgbase-$_locale-$pkgver/4/$_locale/docs/CMakeLists.txt || true
# kde-l10n-sr needs special casing
  sed -e '/kde-runtime/d' -e '/kde-workspace/d' -e '/kdeplasma-addons/d' \
   -i $pkgbase-$_locale-$pkgver/4/$_locale/*/docs/CMakeLists.txt || true
  done
}

build() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}
    
    mkdir -p build/$_locale
    cd build/$_locale
    cmake $srcdir/$pkgbase-$_locale-$pkgver \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr
    make
    cd ../..
  done
}

_package() {
  pkgdesc="$2 localization for KDE Applications"
  cd build/$1
  make DESTDIR="$pkgdir" install
}
