# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: whisky <archlinux.cle(at)gmail.com>
# Contributor: damir <damir@archlinux.org>

pkgname=scim-chewing
pkgver=0.3.5
pkgrel=3
pkgdesc='Traditional Chinese input method module for SCIM'
url='http://chewing.im/'
license=('GPL')
arch=('i686' 'x86_64')
makedepends=('intltool')
depends=('scim' 'libchewing')
source=("https://github.com/chewing/scim-chewing/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('e7ae2356a6ca11c342795e3a14ad7667f76a0af4be23e01ea60acc6d4af72d64')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed '/CHEWING_VERSION=/s/0.3.4/0.4.0/' -i configure
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
