# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kde-l10n
pkgver=16.12.2
pkgrel=1
arch=('any')
url='http://www.kde.org/'
license=('GPL')
depends=('qt5-translations') # needed for RTL languages FS#50780
makedepends=('extra-cmake-modules' 'kdoctools' 'kdelibs' 'automoc4' 'kconfig' 'ki18n' 'qt5-tools' 'python')
options=('docs')

_languages=(
  'ar     "Arabic"'
  'ast    "Asturian"'
  'bg     "Bulgarian"'
  'bs     "Bosnian"'
  'ca     "Catalan"'
  'ca@valencia     "Catalan (Valencian)"'
  'cs     "Czech"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en_GB  "British English"'
  'eo     "Esperanto"'
  'es     "Spanish"'
  'et     "Estonian"'
  'eu     "Basque"'
  'fa     "Farsi"'
  'fi     "Finnish"'
  'fr     "French"'
  'ga     "Irish"'
  'gl     "Galician"'
  'he     "Hebrew"'
  'hi     "Hindi"'
  'hr     "Croatian"'
  'hu     "Hungarian"'
  'ia     "Interlingua"'
  'id     "Indonesian"'
  'is     "Icelandic"'
  'it     "Italian"'
  'ja     "Japanese"'
  'kk     "Kazakh"'
  'km     "Khmer"'
  'ko     "Korean"'
  'lt     "Lithuanian"'
  'lv     "Latvian"'
  'mr     "Marathi"'
  'nb     "Norwegian Bokmal"'
  'nds    "Low Saxon"'
  'nl     "Dutch"'
  'nn     "Norwegian Nynorsk"'
  'pa     "Panjabi/Punjabi"'
  'pl     "Polish"'
  'pt     "Portuguese"'
  'pt_BR  "Brazilian Portuguese"'
  'ro     "Romanian"'
  'ru     "Russian"'
  'sk     "Slovak"'
  'sl     "Slovenian"'
  'sr     "Serbian"'
  'sv     "Swedish"'
  'tr     "Turkish"'
  'ug     "Uyghur"'
  'uk     "Ukrainian"'
  'wa     "Walloon"'
  'zh_CN  "Chinese Simplified"'
  'zh_TW  "Chinese Traditional"'
)

pkgname=()
source=()

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=kde-l10n-${_locale,,}
  
  pkgname+=($_pkgname)
  source+=("http://download.kde.org/stable/applications/$pkgver/src/$pkgbase/$pkgbase-$_locale-$pkgver.tar.xz")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

md5sums=('c41a77e85ed1160516e18e780b59f309'
         'cddb774afdebf95dcebf1a361c9fbfc8'
         '6b197bb3888b04721a80860b403408d6'
         'e6a58dc18bf5b5ffc25318e4a83a84af'
         'b4196566a283b117793a14d5b7d9d836'
         'f605cc81dc32cb9f738a0d42f48382c2'
         '5b3acb6ce8afa73a0e94ad9e8bb7804a'
         '592e8a60fc7aaccb53446c79adc13351'
         '600854184276a1d67d4f7c7f89c4ce29'
         'f79e10c1a5bc9a45ec17be54e162988e'
         '887e6f0e7a2367b6c5f84e25f2339122'
         '146b558fb687efe8e493ae4b3a491698'
         '1390dda18ed8e91c85110cf4ae081f23'
         'fcf6cf75f34b02101200db67c13c00e2'
         'eabc1e76ec04348fd3f112876e7c48a5'
         '4dc20d6ad76f6a3f7329fa4e523220da'
         '118da9d1ffea734d853671864a003098'
         'ff8933ed6d1cda160d39080fe193f46c'
         'a0bc26d171dd95f433274964d49e80ce'
         '577cf9cc51d84738d8ce6967579c2aef'
         'b37c2619a9fda46c9539b36b60750228'
         '061b9e48731bd7c7712169342557170f'
         '13bbbb6c5cdda1e3ba03b47a735919f1'
         'e92b4581be166390862794bcc8e174e5'
         'b6e3a7d035e11b2dd31525ba7e86938c'
         '2dcd63955f396bbfecbdc4cc54b07966'
         '205ca2d600850d9d98bf2bbf6a90bce0'
         'ad89ffbbef989673d4a9b16c3e685f41'
         '8236d6f918db30b0a8aa1987aecb0d06'
         '2c6ecf82d92d21382f8fd380be818243'
         '631bbdecc6842f4eb6f2d91263c80e32'
         '39777d3a145669ae508da3bb03c9ff09'
         'ef15535c29afc63a086439b4440d03b2'
         'e6a1d5debb0904270a6899fe0da7a1fd'
         'ef5e78151946bcb58d61cc5111762f4d'
         '208b43fb5e84cd7d461a39cdaee9c428'
         '60a7c30e7eed5d08a5e303aca2b2f90e'
         '1fc24ba8a9a4e621587f591515079a10'
         '416a57f7a41f8c3c6c9edb8ad629c262'
         '3500f61b31b1dd85c90a46134ce89980'
         'e32acc3d5065fbd6e644c17d222dc540'
         'a65fbf4581bec618d3c1f36e077c5b29'
         'e57621a5f8ab9c064e80a0e5ae7a9607'
         '7abac8d4f82bec3ab8760d6f84149a3a'
         '7a43662550d22d00f57ec3e6d85953bc'
         '992e49c18fa06a7ffe96ecedb988b6ff'
         '5fa3954b24db9e5a6a43e0c2a7836e2f'
         '9ddc18cd0054dcb077ab73cf2d2c3583'
         'f79ee6cf57bb2abe4f0cc1cb9253ed05'
         'da0288f95ec654364cbc35b60f11eba1'
         '6a5d33299943f656a16b0ac6c33e7d85'
         '252ea8b15e2052519ab081185bca4c9d'
         'df7ee59cecb9dc31787e09d6fa3a9a01'
         'fe975832b51371a9d26033b7862c7200'
         '0a7088709085da9aefa0e84120f46f10')

build() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}
    
    mkdir -p build/$_locale
    cd build/$_locale
    cmake $srcdir/$pkgbase-$_locale-$pkgver \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr
    make
    cd ../..
  done
}

_package() {
  pkgdesc="$2 localization for KDE Applications"
  cd build/$1
  make DESTDIR="$pkgdir" install

# Fix conflicts
  rm -fr "$pkgdir"/usr/share/doc/HTML/*/{kdesu,knetattach,kcontrol/kcm_ssl}
}
