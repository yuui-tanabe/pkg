# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Mark Rosenstand <mark@archlinux.org>

pkgname=fuse
pkgver=2.9.7
pkgrel=1
pkgdesc="A library that makes it possible to implement a filesystem in a userspace program."
arch=('i686' 'x86_64')
url='https://github.com/libfuse/libfuse'
license=('GPL2')
depends=('glibc')
makedepends=('pkg-config')
backup=(etc/fuse.conf)
source=(https://github.com/libfuse/libfuse/releases/download/fuse-$pkgver/fuse-$pkgver.tar.gz{,.asc}
	'fuse.conf')
sha1sums=('cd174e3d37995a42fad32fac92f76cd18e24174f'
          'SKIP'
          '3b42e37a741d4651099225987dc40e7f02a716ad')
validpgpkeys=(ED31791B2C5C1613AF388B8AD113FCAC3C4E599F) # Nikolaus Rath <Nikolaus@rath.org>

build() {
  cd fuse-$pkgver

  export MOUNT_FUSE_PATH=/usr/bin
  ./configure --prefix=/usr --libdir=/usr/lib --enable-lib --enable-util --disable-example
  make
}

package() {
  cd fuse-$pkgver

  make DESTDIR=${pkgdir} install

  # Remove init script in wrong path
  # Don't add our own for now, as fusectl fs oopses on 2.6.18
  rm -rf ${pkgdir}/etc/init.d

  # install sample config file
  install -Dm644 ${srcdir}/fuse.conf ${pkgdir}/etc/fuse.conf

  # remove udev rules (is in the udev package}
  rm -rf ${pkgdir}/etc/udev

  # static device nodes are handled by udev
  rm -rf ${pkgdir}/dev
}
